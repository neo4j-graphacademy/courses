= Cypherプロジェクション
:type: quiz

// [.video]
// video::xxxx[youtube,width=560,height=315]


[.transcript]
== はじめに

nativeプロジェクションはスケーラブルで高速ですが、フィルタリングやアグリゲーション機能はCypherほど柔軟ではありません。Cypherプロジェクションは、その名の通り、Cypherを使ってプロジェクション・パターンを定義するため、より柔軟な対応が可能となります。

Cypherプロジェクションは、さらなる柔軟性やカスタマイズが必要な探索的分析や開発段階での利用を意図しています。また、比較的小さなコミュニティやノードの近傍など、グラフのごく一部だけをサブセット化する予定の実稼働環境でも使用できます。

Cypherプロジェクションは、より柔軟でカスタマイズ可能ですが、nativeプロジェクションに比べてパフォーマンスを重視していないため、結果として、より大きなグラフでは、それほど速く、あるいはうまく動作しません。これは、Cypherプロジェクションの使用を検討する際には、常に念頭に置いておくべき重要なトレードオフです。

このレッスンでは、Cypherプロジェクションのシンタックス、応用例、Cypherプロジェクションが役立つ場面、ワークフローの成熟に伴いCypherプロジェクションからnativeプロジェクションに移行する際の一般的な戦略について説明します。

=== Syntax

Cypherのプロジェクションは3つの必須引数を取る。`graphName`, `nodeQuery`, そして `relationshipQuery` である。さらに、オプションの `configuration` パラメータを使用すると、グラフの作成方法をさらに細かく設定することができます。

[opts="header",cols="1,1,8"]
|===
| Name              | Optional | Description
| graphName         | no       | グラフがカタログに保存されるときの名前。
| nodeQuery         | no       | Cypher ステートメントをプロジェクトノードに適用する。クエリーの結果は `id` カラムを含んでいる必要がある。オプションで、ノードのラベルを表す `labels` カラムを指定することができる。その他のカラムはプロパティとして解釈される。
| relationshipQuery | no       | プロジェクトのリレーションシップを表すCypherステートメント。クエリの結果には、 `source` と `target` のカラムが必要である。オプションで、リレーションシップのタイプを表す `type` カラムを指定することができる。その他のカラムはプロパティとして解釈される。
| configuration     | yes      | Cypherプロジェクションを設定するための追加パラメータ。
|===

=== 応用例

前回のレッスンでは、出演した映画の本数から、どの俳優が最も多作であるかを答えました。その代わりに、最近の高収入の映画で共演した他の俳優の数から、どの俳優が最も影響力があるかを知りたかったとしましょう。

この例では、1990年以降に公開された映画を `recent`（最近の映画）、収益が100万ドル以上の映画を `high-grossing`（高収益映画）と呼ぶことにします。

グラフは、nativeプロジェクションの直接投影では、この質問にうまく答えられないように設定されています。しかし、Cypherプロジェクションを使用して適切なノードにフィルターをかけ、集約して、Actorノード間を直接つなぐ `actedWithCount` プロパティを持つ `ACTED_WITH` リレーションシップを作成することができます。

[source, cypher]
----
CALL gds.graph.project.cypher(
  'proj-cypher',
  'MATCH (a:Actor) RETURN id(a) AS id, labels(a) AS labels',
  'MATCH (a1:Actor)-[:ACTED_IN]->(m:Movie)<-[:ACTED_IN]-(a2)
   WHERE m.year >= 1990 AND m.revenue >= 1000000
   RETURN id(a1) AS source , id(a2) AS target, count(*) AS actedWithCount, "ACTED_WITH" AS type'
);
----

これが完了したら、前回のレッスンで行ったようにDegree Centrality（次数中心性）を適用することができます。ただし、次数中心性を `actedWithCount` プロパティで重み付けし、上位 10 件の結果を直接ストリームバックすることになります。  これは、その俳優が最近の高収益(high-grossing)映画で他の俳優と何回共演したかをカウントします。

[source, cypher]
----
CALL gds.degree.stream('proj-cypher',{relationshipWeightProperty: 'actedWithCount'})
YIELD nodeId, score
RETURN gds.util.asNode(nodeId).name AS name, score
ORDER BY score DESC LIMIT 10
----

その結果、予想通り大物俳優が含まれています。

[options="header"]
|===========================
| name               | score
| Robert De Niro     | 123.0
| Bruce Willis       | 120.0
| Johnny Depp        | 102.0
| Denzel Washington  | 99.0
| Nicolas Cage       | 90.0
| Julianne Moore     | 87.0
| Brad Pitt          | 87.0
| Samuel L. Jackson  | 85.0
| George Clooney     | 84.0
| Morgan Freeman     | 84.0
|===========================


=== Cypherプロジェクションを使用する場合

上記の例では、Nativeプロジェクションを使用することを妨げる2つの事柄がありました。この2つは、Cypherプロジェクションを使用する上で最も一般的なケースでもあります。

. *複雑なフィルタリング：* ノード・ラベルとリレーションシップ・タイプだけでなく、グラフをフィルタリングするために、ノードやリレーションシップのプロパティ条件、または他のより複雑なMATCH/WHERE条件を使用すること。
. *マルチホップ・パスの重み付けによる集約:* リレーションシップのプロジェクションでは、`(Actor)-[ACTED_IN]-(Movie)-[ACTED_IN]-(Actor)`パターンを `(Actor)-[ACTED_WITH {actedWithCount}]-(Actor)` パターン（`actedWithCount` はリレーションシップの重みプロパティ）に集約する必要があります。このような投影は、マルチホップパスをソースノードとターゲットノードを結ぶ集約された関係に変換する必要があり、グラフ解析ではよく発生するパターンである。

Cypherプロジェクションには、他にもいくつかの特殊な使用例があります。異なるノードラベルやリレーションシップタイプをマージしたり、プロパティ条件やその他のクエリロジックに基づいてノード間の仮想リレーションシップを定義したりすることができます。


=== Nativeプロジェクションへの移行

Cypherプロジェクションは、これらのパターンの実験やグラフの小さなサブセットの実験には最適ですが、ワークフローが成熟し、グラフのプロジェクションが大きくなり、高速性能がより重要になった場合は、Nativeプロジェクションに移行することをお勧めします。

例えば、上記の計算で、Nativeプロジェクションのcollapse-pathを利用した以下のようなワークフローを使用することができます。この手法では、結果の関係に重み付けをしないので、上位の俳優のランキングは全く同じではありませんが、それでも非常によく似ています。

[source, cypher]
----
//set a node label based on recent release and revenue conditions
MATCH (m:Movie)
WHERE m.year >= 1990 AND m.revenue >= 1000000
SET m:RecentBigMovie;

//native projection with reverse relationships
CALL gds.graph.project('proj-native',
  ['Actor','RecentBigMovie'],
  {
  	ACTED_IN:{type:'ACTED_IN'},
    HAS_ACTOR:{type:'ACTED_IN', orientation: 'REVERSE'}
  }
);

//collapse path utility for relationship aggregation - no weight property
CALL gds.alpha.collapsePath.mutate('proj-native',{
    relationshipTypes: ['ACTED_IN', 'HAS_ACTOR'],
    allowSelfLoops: false,
    mutateRelationshipType: 'ACTED_WITH'
});
----

[source, cypher]
----
//count actors that acted with the most other actors in recent high grossing movies and stream the top 15
CALL gds.degree.stream('proj-native', {nodeLabels:['Actor'], relationshipTypes: ['ACTED_WITH']})
YIELD nodeId, score
RETURN gds.util.asNode(nodeId).name AS name, score
ORDER BY score DESC LIMIT 15
----

[options="header"]
|===========================
| name               | score
| Bruce Willis       | 114.0
| Robert De Niro     | 109.0
| Denzel Washington  | 96.0
| Johnny Depp        | 90.0
| Nicolas Cage       | 86.0
| Julianne Moore     | 84.0
| Samuel L. Jackson  | 82.0
| Morgan Freeman     | 81.0
| Ben Affleck        | 81.0
| Brad Pitt          | 79.0
| Sandra Bullock     | 77.0
| George Clooney     | 77.0
| Julia Roberts      | 75.0
| Matt Damon         | 75.0
| Keanu Reeves       | 74.0
|===========================

CypherからNativeプロジェクションへの移行ワークフローの一般的なヒントは、最初のユースケースに応じて以下のとおりです。:

. *ノードプロパティ条件によるフィルタリング*: プロパティ条件を満たすノードにラベルを追加し、Nativeプロジェクションでフィルタリングできるようにします。
. *リレーションシッププロパティ条件によるフィルタリング:* 可能であれば、プロパティ条件を満たす各リレーションシップについて、ソースノードとターゲットノードにラベルを追加します。そうでない場合は、条件を満たすリレーションシップを把握するために、データモデルに追加のリレーションシップタイプを追加することを検討します。
. *マルチホップパスの集約:*
    .. collapse-pathがあなたのユースケースのニーズを満たせるかどうか見てみましょう。リレーションシップに重み付けをしませんが、結果は重み付き集約と非常によく似ていることが多いです。
    .. 大規模なプロジェクションに関するある種の問題では、類似性(Similality)アルゴリズムや埋め込み(Embedding)アルゴリズムを用いて集約された関係を近似的に表現することが可能である。

その他の複雑な使用例では、Neo4jデータベースのデータモデルに戻ることが多いでしょう。  データモデルを調整し、ノードラベルやリレーションシップの種類によって、データサイエンス・アプリケーションのためにフィルタリングしたいデータをよりよく区別することは可能でしょうか？  これは、特定のパスを単一の関係に集約したり、より多くのノードラベルや関係タイプを開発したり、他のタイプの変換を含むかもしれません。

=== 理解度チェック

include::questions/1-graph-projection-using-cypher.adoc[leveloffset=+1]

include::questions/2-cypher-projection-use-cases.adoc[leveloffset=+1]

include::questions/3-cypher-projection-usage.adoc[leveloffset=+1]



[.summary]
== まとめ

このレッスンでは、Cypherプロジェクションについて学びました。Cypherプロジェクションとは何か、どのように、いつ使うのか、Nativeプロジェクションとの長所と短所、CypherとNativeプロジェクションの間の移行方法について学びました。

次のレッスンでは、独自のCypherプロジェクションの作成に挑戦していただきます。

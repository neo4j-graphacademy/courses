= リレーションシップの作成
:type: quiz
:order: 3

// [.video]
// video::9npLX5us1DU[youtube,width=560,height=315]


//https://youtu.be/9npLX5us1DU


////

2-Writing-CreatingRelationships

https://docs.google.com/document/d/1sZRzA7uqqkjcYee_vQM-KRBlILhkyZvEQa_PAX4CdHg/edit?usp=sharing

////


[.transcript]
== 2つのノード間にリレーションシップを作成する

このレッスンでは、グラフ内の既存のノード間に関係を作成するためのCypher句の書き方を学びます。

`MERGE` を使ってグラフにノードを作成するのと同じように、 `MERGE` を使って2つのノード間にリレーションシップを作成します。
まず、リレーションシップを作成する2つのノードを参照する必要があります。
2つのノードの間にリレーションシップを作成する場合には、以下の要素が必要になります。

* タイプ
* 方向

例えば、_Person_ と _Movie_ というノードがすでに存在している場合、それらの間のリレーションを作成する前に `MATCH` 節を使用してそれらを見つけることができます。

[source,cypher]
----
MATCH (p:Person {name: 'Michael Cain'})
MATCH (m:Movie {title: 'The Dark Knight'})
MERGE (p)-[:ACTED_IN]->(m)
----

ここでは、リレーションシップを作成したい2つのノードを見つけます。
そして、見つかったノードを参照し、_ACTED_IN_ リレーションを作成します。

以下のように、この関係が存在することを確認することができます。

[source,cypher]
----
MATCH (p:Person {name: 'Michael Cain'})-[:ACTED_IN]-(m:Movie {title: 'The Dark Knight'})
RETURN p, m
----

Neo4j Browserでは、デフォルトで、ノード間にリレーションシップがある場合、そのノード間を視覚化します。

クエリエンジンは、リレーションの方向に関係なく、接続されているすべてのノードを検索するので、`MATCH` で方向を指定する必要がないことにも注意してください。

例えば、次のようなリレーションシップパターンを指定したとします。

[source,cypher]
----
MATCH (p:Person {name: 'Michael Cain'})<-[:ACTED_IN]-(m:Movie {title: 'The Dark Knight'})
RETURN p, m
----

このクエリは、_Person_ ノードと _ACTED_IN_ の関係を持つノードがグラフ内に存在しないため、ノードを返しません。

=== 複数の節を使用したノードとリレーションの作成

一つのCypherコードブロックの中で、複数の `MERGE` 節を連結させることもできます。

[source,cypher]
----
MERGE (p:Person {name: 'Chadwick Boseman'})
MERGE (m:Movie {title: 'Black Panther'})
MERGE (p)-[:ACTED_IN]-(m)
----

このコードでは、2つのノードとそれらの間のリレーションシップを作成します。
変数 _p_ と _m_ を指定しているので、コード内でそれらを使って2つのノード間のリレーションを作成することができます。

[TIP]
====
リレーションシップを作成するこの `MERGE` 節では、リレーションシップの向きを指定していないことに注意してください。
デフォルトでは、リレーションシップを作成する際に方向を指定しない場合、常に左から右と見なされます。
====

このリレーションシップは、次のように確認することができます。

[source,cypher]
----
MATCH (p:Person {name: 'Chadwick Boseman'})-[:ACTED_IN]-(m:Movie {title: 'Black Panther'})
RETURN p, m
----

=== `MERGE` を使って一つの節でノードとリレーションシップを作成する。

`MERGE` が行うのは、ノードやリレーションシップがグラフに存在しない場合に、それを作成することです。

このコードでは、ノードとリレーションシップの作成に成功しています。

[source,cypher]
----
MERGE (p:Person {name: 'Emily Blunt'})-[:ACTED_IN]->(m:Movie {title: 'A Quiet Place'})
RETURN p, m
----

このCypherコードを複数回実行しても、新しいノードやリレーションシップが作成されることはありません。


== 理解度チェック

include::questions/1-merge-keyword.adoc[leveloffset=+1]

include::questions/2-type-direction-for-relationship.adoc[leveloffset=+1]

include::questions/3-complete-code.adoc[leveloffset=+1]

[.summary]
== まとめ

このレッスンでは、グラフにリレーションシップを作成する方法を学びました。
次の課題では、ノード間にリレーションシップを作成できることを実演します。


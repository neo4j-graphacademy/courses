= データを削除する
:type: quiz
:order: 9

// [.video]
// video::9UdAfdG5Gag[youtube,width=560,height=315]


////

2-Writing-DeletingData

https://docs.google.com/document/d/1IgJFtqQ0epVu3ywM7cNgbe6nuTxpz-TVTarGV74-pqs/edit?usp=sharing

////
[.transcript]
== データの削除
Neo4jデータベースでは、削除することができます。

* ノード
* リレーションシップ
* プロパティ
* ラベル

データベース内のデータを削除するには、まずデータを取得し、それから削除する必要があります。
ノードやリレーションシップからプロパティを削除する方法については、すでに学習済みでしょう。

=== ノードの削除

_Jane Doe_の _Person_ ノードを作成したとします。

このCypherのコードを実行して、ノードを作成します。

[source,cypher]
----
MERGE (p:Person {name: 'Jane Doe'})
----

このノードを削除するには、以下の手順でノードを取得します。そして、そのノードを参照しながら削除することができます。

[source,cypher]
----
MATCH (p:Person)
WHERE p.name = 'Jane Doe'
DELETE p
----

=== 関係の削除

映画「マトリックス」の俳優として追加された _Jane Doe_ ノードが再びあったとします。
このコードを実行して、ノードとリレーションシップを作成します。

[source,cypher]
----
MATCH (m:Movie {title: 'The Matrix'})
MERGE (p:Person {name: 'Jane Doe'})
MERGE (p)-[:ACTED_IN]->(m)
RETURN p, m
----

このコードは1つのノードを作成し、_Jane Doe_ から _The Matrix_ へのリレーションシップを作成します。

_Jane Doe_ ノードをグラフに残し、リレーションシップを削除するには、リレーションシップを取得し、それを削除します。

[source,cypher]
----
MATCH (p:Person {name: 'Jane Doe'})-[r:ACTED_IN]->(m:Movie {title: 'The Matrix'})
DELETE r
RETURN p, m
----

このリレーションシップを再現するCypherのコードを実行してください。

[source,cypher]
----
MATCH (p:Person {name: 'Jane Doe'})
MATCH (m:Movie {title: 'The Matrix'})
MERGE (p)-[:ACTED_IN]->(m)
RETURN p, m
----

_Jane Doe_ ノードを削除しようとすると、グラフ内にリレーションシップがあるため、エラーが発生します。

このCypherのコードを実行してみてください。

[source,cypher]
----
MATCH (p:Person {name: 'Jane Doe'})
DELETE p
----

エラーが表示されるはずです。Neo4jはグラフ内の孤立したリレーションシップを防ぎます。


=== ノードとそのリレーションシップの削除

Neo4jは、ノードが入出力リレーションシップを持っている場合、そのノードを削除できない機能を提供します。
これにより、グラフが孤立したリレーションシップになることを防いでいます。

このCypherのコードを実行してみてください。

[source,cypher]
----
MATCH (p:Person {name: 'Jane Doe'})
DETACH DELETE p
----

このコードでは、リレーションシップと _Person_ ノードを削除しています。

また、このコードでデータベース内のすべてのノードとリレーションシップを削除することもできます。

[NOTE]
グラフのノードとリレーションシップは、次のチャレンジに必要なので、すべて削除しないでください。

[source,cypher,role=nocopy noplay]
----
MATCH (n)
DETACH DELETE n
----

[IMPORTANT]
大規模なデータベースでこれを行おうとすると、メモリを使い果たしてしまうので、比較的小規模なデータベースでのみ行うようにしてください。

=== ラベルの削除

ノードのラベルは最低でも1つ、最大でも4つまでとするのがベストプラクティスです。

このCypherのコードを実行して、グラフにPersonノードを作成します。

[source,cypher]
----
MERGE (p:Person {name: 'Jane Doe'})
RETURN p
----

次に、このコードを実行すると、このノードに新しいラベルが追加されます。

[source,cypher]
----
MATCH (p:Person {name: 'Jane Doe'})
SET p:Developer
RETURN p
----

新しく追加されたラベル「Developer」を削除するには、`REMOVE` 句を使用します。次のコードを実行してください。

[source,cypher]
----
MATCH (p:Person {name: 'Jane Doe'})
REMOVE p:Developer
RETURN p
----

Jane Doe ノードには Person と Developer という2つのラベルがあります。そのノードを見つけるために `MATCH` を使うことができます。
`MATCH (p:Developer {name: 'Jane Doe'})` や `MATCH (p:Person:Developerr {name: 'Jane Doe'})` を指定すれば、同じノードを見つけることができることに注意してください。
そのノードへの参照が得られたら、`REMOVE` 節でラベルを削除することができます。

そして最後に、以下のコードを実行して、Jane Doeノードを削除します。

[source,cypher]
----
MATCH (p:Person {name: 'Jane Doe'})
DETACH DELETE p
----

この場合、リレーションシップを作成していないので、`DELETE p`も機能することに注意してください。

=== グラフに存在するラベルは？

このコードは、グラフに定義されたすべてのノードラベルを返す。

[source,cypher]
----
CALL db.labels()
----

== 理解度の確認

include::questions/1-complete-code.adoc[leveloffset=+1]

include::questions/2-delete-all.adoc[leveloffset=+1]

[.summary]
== まとめ

このレッスンでは、グラフからノードとリレーションシップを削除する方法を学びました。
次の課題では、グラフからノードを削除することを実演します。

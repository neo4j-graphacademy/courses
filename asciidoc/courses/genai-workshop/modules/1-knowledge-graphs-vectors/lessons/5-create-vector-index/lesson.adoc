= Create a Vector Index
:order: 5
:type: challenge
:sandbox: true
:slides: true

[.slide.col-60-40]
== Movie posters

[.col]
====
Your next task is to create a vector index using Cypher.

You previously used a vector index to find similar text; you can also use a vector index to find similar images.

GraphAcademy has loaded a sample of 1000 movie poster embeddings into the sandbox. 

Each movie has a URL to a poster image and embeddings for the poster:

[source, cypher]
MATCH (m:Movie {title: "Toy Story"}) 
RETURN m.title, m.poster, m.posterEmbedding
====

[.col]
====
image:https://image.tmdb.org/t/p/w440_and_h660_face/uXDfjJbdP4ijW5hWSBrPrlKpxab.jpg[Toy Story movie poster]
====

[.slide.col-2]
== Create a vector index

[.col]
====
To search the movie poster embeddings, you must create a vector index. 
Review the following Cypher to create the vector index before running it.

[source, cypher]
----
CREATE VECTOR INDEX moviePosters IF NOT EXISTS
FOR (m:Movie)
ON m.posterEmbedding
OPTIONS {indexConfig: {
 `vector.dimensions`: 512,
 `vector.similarity_function`: 'cosine'
}}
----
====

[.col]
====
You should note the following about the index:

- It is named `moviePosters`
- It is against the `posterEmbedding` properties on `Movie` nodes
- The vector has `512` dimensions
- The function used to compare vectors is `cosine`
====

[.transcript-only]
====
[%collapsible]
.More about dimensions
=====
The model used to create the embeddings determines the number of dimensions in the vector.

In this case, we used the link:https://openai.com/research/clip[OpenAI Clip Model^], which has 512 dimensions.

We created the movie plot embeddings using link:https://platform.openai.com/docs/guides/embeddings/embedding-models[Open AI's text-embedding-ada-002 model^], which has 1536 dimensions.
=====
====

Run the Cypher to create the vector index.

[.slide.discrete]
== Check the index exists

Check that you created the index successfully using the `SHOW INDEXES` command.

.Show Indexes
[source,cypher]
----
SHOW VECTOR INDEXES
----

You should see a result similar to the following:

.Show Indexes Result
|===
| id | name | state | populationPercent | type
|4 | "moviePosters" | "ONLINE" | `100.0` | "VECTOR"
|===

Once the `state` is listed as "ONLINE", the index will be ready to query.

[.transcript-only]
====
The `populationPercentage` field indicates the proportion of node and property pairing.
When the `populationPercentage` is `100.0`, all the movie embeddings have been indexed.
====

[.slide.col-2]
== Similar posters

[.col]
====
You can use the `db.index.vector.queryNodes` procedure to find similar movie posters.

[source, cypher]
----
MATCH (m:Movie{title: "Babe"})

CALL db.index.vector.queryNodes
    ('moviePosters', 6, m.posterEmbedding)
YIELD node, score

RETURN node.title, node.poster, score;
----
====

[.col]
====
image::images/babe-similar-posters.jpg[3 movie posters, Babe, Lassie, Before the Rain with similar images]
====

[.slide.discrete]
== Find a similar poster

Pick a different movie and update the Cypher query to find similar posters.

You can view the movies that have a poster embedding using this Cypher:

[source, cypher]
----
MATCH (m:Movie)
WHERE m.posterEmbedding IS NOT NULL
RETURN m.title, m.poster
----

[.next]
== Continue

When you are ready, you can move on to the next task.

read::Move on[]

[.summary]
== Summary

You learned how to create a vector index in Neo4j.

Next, you will learn how to model unstructured data as a graph.
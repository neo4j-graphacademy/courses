= Create a Vector Index
:order: 5
:type: challenge

Create vector index on movie posters



Can create a vector index in Neo4j

Understands that vectors can be used for more than just text





== Movie posters

GraphAcademy has loaded a dataset of movie posters into the sandbox. 
Each movie has a URL to a poster image:

[source, cypher]
MATCH (m:Movie {title: "Toy Story"}) 
RETURN m.title, m.poster

image:https://image.tmdb.org/t/p/w440_and_h660_face/uXDfjJbdP4ijW5hWSBrPrlKpxab.jpg[Toy Story movie poster,width=250,align=center]

The data also contains embeddings for each poster:

[source, cypher]
MATCH (m:Movie {title: "Toy Story"}) 
RETURN m.title, m.posterEmbedding

== Create a vector index

To search the movie poster embeddings, you must create a vector index. 
Review the following Cypher to create the vector index before running it:

[source, cypher]
----
CREATE VECTOR INDEX moviePosters IF NOT EXISTS
FOR (m:Movie)
ON m.posterEmbedding
OPTIONS {indexConfig: {
 `vector.dimensions`: 512,
 `vector.similarity_function`: 'cosine'
}}
----

You should note the following about the index:

- It is named `moviePosters`
- It is against the `posterEmbedding` properties on `Movie` nodes
- The vector has `512` dimensions
- The function used to compare vectors is `cosine`

[%collapsible]
.More about dimensions
====
The number of dimensions in the vector is determined by the model used to create the embeddings.

In this case, we used the link:https://openai.com/research/clip[OpenAI Clip Model^], which has 512 dimensions.

We created the movie plot embeddings using link:https://platform.openai.com/docs/guides/embeddings/embedding-models[Open AI's text-embedding-ada-002 model^], which has 1536 dimensions.

The model used to create the embeddings determines the number of dimensions in the vector.
====

Run the Cypher to create the vector index.

Check that you created the index successfully using the `SHOW INDEXES` command.

.Show Indexes
[source,cypher]
----
SHOW INDEXES WHERE type = "VECTOR"
----

Once the `state` is listed as "ONLINE", the index will be ready to query.

The `populationPercentage` field indicates the proportion of node and property pairing.
When the `populationPercentage` is `100.0`, all the movie embeddings have been indexed.

== Similar posters

In the same way, you can use a vector index to find similar text; you can use a vector index to find similar images.

[source, cypher]
----
MATCH (m:Movie{title: "Babe"})

CALL db.index.vector.queryNodes('moviePosters', 6, m.posterEmbedding)
YIELD node, score

RETURN node.title, node.poster, score;
----

image:images/babe-similar-posters.jpg[3 movie posters, Babe, Lassie, Before the Rain with similar images,width=700,align=center]

Pick a different movie and write a similar Cypher query to find similar posters.

[source, cypher]
.Find all movies
----
MATCH (m:Movie)
RETURN m.title
----

read::Continue[]


[.summary]
== Summary

You learned ...
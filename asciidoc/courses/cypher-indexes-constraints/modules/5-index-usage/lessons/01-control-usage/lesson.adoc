= Controlling Index Usage
:type: quiz
:sandbox: true

[.video]
video::KB7lzIb_2oU[youtube,width=560,height=315]

//https://youtu.be/KB7lzIb_2oU
//delete old video: https://youtu.be/Ed187DcFPWE


[.transcript]
== Single index used by default

A `MATCH` clause will use a single index by default.

To illustrate this, execute this code multiple times and observe the lowest elapsed time.

[source,cypher]
----
PROFILE MATCH
(p:Person)-[:ACTED_IN]->(m:Movie)<-[:DIRECTED]-(p2:Person)
WHERE
p.name CONTAINS 'John'
AND
p2.name CONTAINS 'George'
RETURN p.name, p2.name,  m.title
----

// video redo
// need to re-record this with V5 version
It should return 17 rows and have an elapsed time of ~7 ms in the Plan view.
Notice that the TEXT index is used to anchor the query on the p2 end of the path.
This is because the index is more favorably populated with fewer actors named George so this will reduce the number of rows returned.

[IMPORTANT]
.Consult the Manual
--
Before you create the indexes for your application, please read the section in the https://neo4j.com/docs/cypher-manual/current/query-tuning/indexes/[Cypher Reference Manual^] that has many examples how indexes are used.
--

=== Specifying a query hint

In general, the query planner does a good job in determining which index to use to anchor a query.
You can force a how an index will be used by specifying `USING INDEX`, called a query hint.

Execute this code (multiple times) that tells the query planner to use p as the anchor of the query and use the index for that end of the path:

[source,cypher]
----
PROFILE MATCH
(p:Person)-[:ACTED_IN]->(m:Movie)<-[:DIRECTED]-(p2:Person)
USING INDEX p:Person(name)
WHERE
p.name CONTAINS 'John'
AND
p2.name CONTAINS 'George'
RETURN p.name, p2.name,  m.title
----

Notice that with this query, the anchor is the "p" end of the query path and the TEXT index is used.
Notice also that this query does not perform as well as the default index usage.
It has the same elapsed time, but requires more db hits.

[NOTE]
.Verify Query Performance
--
If you add `USING INDEX` clauses to your cypher code, you must ensure that it makes your query perform better.
A hint with a specified index type is only possible when the planner knows that using an index of the specified type does not change the results.
You must provide query planner hints carefully in your code as described https://neo4j.com/docs/cypher-manual/current/query-tuning/using/[here^]
--

=== Using multiple indexes

Depending upon your use cases and graph, it may be better  to use more than one index for a query.

Execute this code (multiple times) that will enable index usage for both ends of the query path.

[source,cypher]
----
PROFILE MATCH
(p:Person)-[:ACTED_IN]->(m:Movie)<-[:DIRECTED]-(p2:Person)
USING INDEX p:Person(name)
USING INDEX p2:Person(name)
WHERE
p.name CONTAINS 'John'
AND
p2.name CONTAINS 'George'
RETURN p.name, p2.name,  m.title
----

// video redo
// need to re-record this with V5 version
This query executes about the same at ~7 ms but with fewer db hits.

This query uses the TEXT index to find the Person nodes that contain John and it uses the TEXT index to find the Person nodes that contain George.
Then it does a join to return the movie titles these nodes share.
Not all queries will benefit from query hints so you should be careful to fully test your queries/indexes.

[NOTE]
.Consistently check query performance
--
As you develop your application, it will be very important to profile the most important queries of your application, add the appropriate indexes, and tune your queries as an iterative process.
--

=== Query hints for relationships

We already have an index on the RATED.rating property.

Run this query multiple times. It does not use any index, even the one defined for the RATED.rating property:

[source,cypher]
----
PROFILE MATCH
(u:User)-[r:RATED]->(m:Movie)
WHERE
u.name CONTAINS 'Johnson'
AND
r.rating = 5
RETURN u.name, r.rating, m.title
----

Suppose we want to see if using the index on the relationship might help.
Here we specify that we want to use this index.

Execute this code multiple times:

[source,cypher]
----
PROFILE MATCH
(u:User)-[r:RATED]->(m:Movie)
USING INDEX r:RATED(rating)
WHERE
u.name CONTAINS 'Johnson'
AND
r.rating = 5
RETURN u.name, r.rating, m.title
----

This execution uses the index on the RATED relationship, but as you can see, it is not better.
That is why the query planner chose not to use the index.

You must use caution and test any query hints you intend to use in your application.

== Check your understanding

include::questions/1-complete-query.adoc[leveloffset=+1]
include::questions/2-select-answer.adoc[leveloffset=+1]

[.summary]
== Summary

In this lesson, you learned how to provide query hints for index usage.
In the next Challenge, you will add a query hint to a query.
= Controlling Index Usage
:type: quiz
:sandbox: true

//[.video]
//video::Po4k7vXGBIE[youtube,width=560,height=315]

//https://youtu.be/Po4k7vXGBIE

[.transcript]
== Single index used by default

A `MATCH` clause will use a single index by default.

To illustrate this, execute this code:

[source,cypher]
----
PROFILE MATCH
(p:Person)-[:ACTED_IN]->(m:Movie)<-[:DIRECTED]-(p2:Person)
WHERE
p.name CONTAINS 'John'
AND
p2.name CONTAINS 'George'
RETURN p.name, p2.name,  m.title
----

It should return 17 rows and have an elapsed time of ~15 ms.
Notice that the TEXT index is used to anchor the query on the p2 end of the path.
This is because the index is more favorably populated with fewer actors named George so this will reduce the number of rows returned.

[IMPORTANT]
--
Before you create the indexes for your application, please read the section in the https://neo4j.com/docs/cypher-manual/current/query-tuning/indexes/[Cypher Reference Manual^] that has many examples how indexes are used.
--

== Specifying a query hint

In general, the query planner does a good job in determining which index to use to anchor a query.
You can force a how an index will be used by specifying `USING INDEX`, called a query hint.

Execute this code that tells the query planner to use p as the anchor of the query and use the index for that end of the path:

[source,cypher]
----
PROFILE MATCH
(p:Person)-[:ACTED_IN]->(m:Movie)<-[:DIRECTED]-(p2:Person)
USING INDEX p:Person(name)
WHERE
p.name CONTAINS 'John'
AND
p2.name CONTAINS 'George'
RETURN p.name, p2.name,  m.title
----

Notice that with this query, the anchor is the "p" end of the query path and the TEXT index is used.
Notice also that this query does not perform as well as the default index usage.

[NOTE]
--
If you add `USING INDEX` clauses to your cypher code, you must ensure that they do not change the results returned and that having the `USING INDEX` makes your query perform better.
You must provide query planner hints carefully in your code as described https://neo4j.com/docs/cypher-manual/current/query-tuning/using/[here^]
--

== Using multiple indexes

Depending upon your use cases and graph, it may be better  to use more than one index for a query.

Execute this code that will enable index usage for both ends of the query path.

[source,cypher]
----
PROFILE MATCH
(p:Person)-[:ACTED_IN]->(m:Movie)<-[:DIRECTED]-(p2:Person)
USING INDEX p:Person(name)
USING INDEX p2:Person(name)
WHERE
p.name CONTAINS 'John'
AND
p2.name CONTAINS 'George'
RETURN p.name, p2.name,  m.title
----

This query executes a little faster at ~11 ms.

This query uses the TEXT index to find the Person nodes that contain John and it uses the TEXT index to find the Person nodes that contain George.
Then it does a join to return the movie titles these nodes share.
Not all queries will benefit from query hints so you should be careful to fully test your queries/indexes.

[NOTE]
--
As you develop your application, it will be very important to profile the most important queries of your application, add the appropriate indexes, and tune your queries as an iterative process.
--

== Query hints for relationships

We already have an index on the RATED.rating property.

When this query executes, it does not use any index, even the one defined or the RATED.rating property:

[source,cypher]
----
PROFILE MATCH
(u:User)-[r:RATED]->(m:Movie)
WHERE
u.name CONTAINS 'Johnson'
AND
r.rating = 5
RETURN u.name, r.rating, m.title
----

Suppose we want to see if using the index on the relationship might help.
Here we specify that we want to use this index. Execute this code:

[source,cypher]
----
PROFILE MATCH
(u:User)-[r:RATED]->(m:Movie)
USING INDEX r:RATED(rating)
WHERE
u.name CONTAINS 'Johnson'
AND
r.rating = 5
RETURN u.name, r.rating, m.title
----

This execution uses the index on the RATED relationship, but as you can see, it is not better.
That is why the query planner chose not to use the index.

You must use caution and test any query hints you intend to use in your application.

include::questions/1-complete-query.adoc[leveloffset=+1]
include::questions/2-select-answer.adoc[leveloffset=+1]

[.summary]
== Summary

In this lesson, you learned how to provide query hints for index usage.
In the next Challenge, you will add a query hint to a query.
= Creating Single Property Indexes
:type: quiz
:sandbox: true

//[.video]
//video::Po4k7vXGBIE[youtube,width=560,height=315]

//https://youtu.be/Po4k7vXGBIE

[.transcript]
== Creating b-tree indexes

In this lesson you will learn how to create a single property b-tree index on a node property or relationship property to optimize queries.

== Listing indexes

You have already learned how to list the constraints in the graph:

[source,cypher]
----
SHOW CONSTRAINTS
----

There is another command that lists the indexes in the graph:

[source,cypher]
----
SHOW INDEXES
----

`SHOW INDEXES` provides information about the implementation of the index.
When you execute `SHOW INDEXES` on the current graph you have been working with, you see that the constraints defined in the graph are b-tree indexes.
That is, the implementation of a constraint in the graph is as a b-tree, along with the uniqueness and existence characteristics of the index.

[IMPORTANT]
--
A graph will always contain a LOOKUP index that you see when you list the indexes.
You should never drop this index as it is used to quickly find nodes by their labels and relationships by their types in the graph.
--

== Syntax for creating a b-tree index for a single property of a node

Here is the syntax for creating a b-tree index for a single property of a node:

[source,cypher,rel="norun nocopy"]
----
CREATE INDEX <index_name> IF NOT EXISTS FOR (x:<node_label>) ON x.<property_key>
----

You specify the name of the index, the node label it will be associated with, and the name of the property.

* If an index already exists in the graph with the same name, no index is created.
* If an index does not exist in the graph with the same name:
** No index is created if there already is an index for that node label and property key.
** Otherwise, the index is created.

=== Creating the b-tree index for a single property of a node

Suppose that we want this type of query to perform its best:

[source,cypher]
----
PROFILE MATCH (m:Movie)
WHERE m.title STARTS WITH "Toy"
RETURN m.title
----

If you execute this query and examine the plan produced by the `PROFILE`, you will see that it required 18,251 total db hits.
The query plan begins with a node by label scan.
To test the `WHERE m.title STARTS WITH "Toy"` predicate, the query engine must examine all title properties of every Movie node.

To improve the performance of this query, we create a b-tree index on the Movie.title property.

Execute this code to create the b-tree index for the Movie.title property:

[source,cypher]
----
CREATE INDEX Movie_title IF NOT EXISTS FOR (x:Movie) ON (x.title)
----

Repeat the above query with the `PROFILE`. You should see that with the index, only 8 db hits occur.
The query plan now starts with the NodeIndexSeekByRange() operation and uses the b-tree index you just created.

== Always profile before you deploy your application!

We cannot understate how important it is to profile your queries, especially queries that are most important to your application.

Suppose in our application, we wanted to have the query be case-insensitive.
The new query would be:

[source,cypher]
----
PROFILE MATCH (m:Movie)
WHERE toLower(m.title) STARTS WITH "toy"
RETURN m.title
----

== Syntax for creating a b-tree index for a single property of a relationship

Here is the syntax for creating a b-tree index for a single property of a relationship:

[source,cypher,rel="norun nocopy"]
----
CREATE INDEX <index_name> IF NOT EXISTS FOR ()-[x:<RELATIONSHIP_TYPE>]-() ON (x.<property_key>)
----

You specify the name of the index, the relationship type it will be associated with, and the name of the property.

* If an index already exists in the graph with the same relationship type, no index is created.
* If an index does not exist in the graph with the same name:
** No index is created if there already is an index for that relationship type and property key.
** Otherwise, the index is created.

You will create a b-tree index on a relationship type property in one of the next Challenges.

== Dropping indexes

Previously, you learned how to drop constraints in the graph.
You use `DROP INDEX <index_name>` to drop an index by its name.

include::questions/1-complete-query.adoc[leveloffset=+1]
include::questions/2-select-answer.adoc[leveloffset=+1]

[.summary]
== Summary

In this lesson, you learned how to create a b-tree index for a property of a node.
In the next challenge, you will create another b-tree index and test it.

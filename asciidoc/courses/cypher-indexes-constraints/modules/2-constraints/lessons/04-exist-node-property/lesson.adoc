= Creating Existence Constraints for Node Properties
:type: quiz
:sandbox: true

//[.video]
//video::Po4k7vXGBIE[youtube,width=560,height=315]

//https://youtu.be/Po4k7vXGBIE

[.transcript]

//TBD
== Existence constraints for nodes

Depending on your application needs, you may want to ensure that some nodes with a given label **always** have a particular property.
For example, a Person node **must** have a property, name.
The name need not be unique for all Person nodes, but the application does not permit a Person node without a name property.

[NOTE]
--
Existence constraints are only available in Enterprise Edition.
---

== Syntax for creating an existence constraint on a node

Here is the syntax for creating an existence constraint on a node property:

[source,cypher,rel="norun nocopy"]
----
CREATE CONSTRAINT <constraint_name> IF NOT EXISTS FOR (x:<node_label>) REQUIRE x.<property_key> IS IS NOT NULL
----

You specify the name of the constraint, the node label it will be associated with, and the name of the property that must exist for nodes with that label.

* If a constraint already exists in the graph with the same name, no constraint is created.
* If a constraint does not exist in the graph with the same name:
** No constraint is created if there already is an existence constraint for that node label and property key.
** Otherwise, the constraint is created.

=== Creating the existence constraint

Suppose we want to enforce that all _Person_ nodes must have a value for a **name** property.
Execute this code to create an existence constraint for this property:

[source,cypher]
----
CREATE CONSTRAINT Person_name_exists IF NOT EXISTS FOR (x:Person) REQUIRE x.name IS NOT NULL
----

This code creates the existence constraint named Person_name_exists.

[NOTE]
You typically create constraints before the data is loaded into the graph, but in our case, we already have the data in the graph.

=== Constraint creation failure

Not all movies in the graph for this course contain a value for the _plot_ property.
The creation of this constraint should fail:

[source,cypher]
----
CREATE CONSTRAINT Movie_plot_exists IF NOT EXISTS FOR (x:Movie) REQUIRE x.plot IS NOT NULL
----

=== Node creation or update failure

We have created the Person_name_exists constraint in the graph.
If we attempt to create a Person node without a name, an error will occur and the node will not be created:

[source,cypher]
----
MERGE (x:Person {tmdbId:9999999})
SET x.born = 2022
----

If we attempt to remove a name property from an existing node, an error will occur:

[source,cypher]
----
MATCH (x:Person {name: 'Tom Hanks'})
SET x.name = null
----

=== Listing constraints in the graph

At this point, you should have multiple uniqueness constraints and a single existence constraint in the graph:

[source,cypher]
----
SHOW CONSTRAINTS
----

include::questions/1-complete-query.adoc[leveloffset=+1]
include::questions/2-select-answer.adoc[leveloffset=+1]

[.summary]
== Summary

In this lesson, you learned how to create an existence constraint for node properties.
In the next challenge, you will create more existence constraints.

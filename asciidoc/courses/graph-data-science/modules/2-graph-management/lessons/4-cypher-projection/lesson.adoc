= Cypher Projections
:type: quiz

// [.video]
// video::xxxx[youtube,width=560,height=315]


[.transcript]
== Introduction

While the native projection is scalable and fast, its filtering and aggregation capabilities aren’t as flexible as Cypher. The Cypher projection, as its name implies, uses Cypher to define the projection pattern, and as such, enables more flexibility.

Cypher projections are intended to be used in exploratory analysis and developmental phases where additional flexibility and/or customization is needed.  They can also work in production settings where you plan to subset only a small portion of the graph, such as a relatively small community or neighborhood of nodes.

While Cypher projections offer more flexibility and customization, they have a diminished focus on performance relative to native projections and as a result won’t perform as quickly or as well on larger graphs. This is a key trade-off to keep in mind whenever you consider using Cypher projections.

In this lesson, we will go over the cypher projection syntax, an applied example, where cypher projections are useful, and common strategies for transition from Cypher to native projections as workflows mature.

=== Syntax

A Cypher projection takes three mandatory arguments: `graphName`, `nodeQuery`, and `relationshipQuery`. In addition, the optional configuration parameter allows us to further configure graph creation.

[opts="header",cols="1,1,8"]
|===
| Name              | Optional | Description
| graphName         | no       | The name under which the graph is stored in the catalog.
| nodeQuery         | no       | Cypher query to project nodes. The query result must contain an `id` column. Optionally, a `labels` column can be specified to represent node labels. Additional columns are interpreted as properties.
| relationshipQuery | no       | Cypher query to project relationships. The query result must contain `source` and `target` columns. Optionally, a `type` column can be specified to represent relationship type. Additional columns are interpreted as properties.
| configuration     | yes      | Additional parameters to configure the Cypher projection.
|===

=== Applied Example

In the last lesson we answered which actors were most prolific based on the number of movies they acted in. Suppose instead we wanted to know which actors are the most influential in terms of the number of other actors they have been in recent, high grossing, movies with.

For the sake of this example, we will call a movie “recent” if it was released on or after 1990, and high-grossing if it had revenue >= $1M.

The graph is not set up to answer this question well with a direct native projection. However, we can use a cypher projection to filter to the appropriate nodes and perform an aggregation to create an `ACTED_WITH` relationship that has a `actedWithCount` property going directly between actor nodes.

[source, cypher]
----
CALL gds.graph.project.cypher(
  'proj-cypher',
  'MATCH (a:Actor) RETURN id(a) AS id, labels(a) AS labels',
  'MATCH (a1:Actor)-[:ACTED_IN]->(m:Movie)<-[:ACTED_IN]-(a2)
   WHERE m.year >= 1990 AND m.revenue >= 1000000
   RETURN id(a1) AS source , id(a2) AS target, count(*) AS actedWithCount, "ACTED_WITH" AS type'
);
----

Once that is done we can apply degree centrality like we did last lesson. Except we will weight the degree centrality by `actedWithCount` property and also directly stream the top 10 results back.  This counts how many times the actor has acted with other actors in recent, high grossing movies.

[source, cypher]
----
CALL gds.degree.stream('proj-cypher',{relationshipWeightProperty: 'actedWithCount'})
YIELD nodeId, score
RETURN gds.util.asNode(nodeId).name AS name, score
ORDER BY score DESC LIMIT 10
----

The results include some big name actors as we would expect.

[options="header"]
|===========================
| name               | score
| Robert De Niro     | 123.0
| Bruce Willis       | 120.0
| Johnny Depp        | 102.0
| Denzel Washington  | 99.0
| Nicolas Cage       | 90.0
| Julianne Moore     | 87.0
| Brad Pitt          | 87.0
| Samuel L. Jackson  | 85.0
| George Clooney     | 84.0
| Morgan Freeman     | 84.0
|===========================


=== When To Use Cypher Projections

In the above example, there were two things that prevented us from directly using a native projection. They also happen to be two of the most common cases for using Cypher Projections.

. *Complex Filtering:* Using node and/or relationship property conditions or other more complex MATCH/WHERE conditions to filter the graph, rather than just node label and relationship types.
. *Aggregating Multi-Hop Paths with Weights:* The relationship projection required aggregating the `(Actor)-[ACTED_IN]-(Movie)-[ACTED_IN]-(Actor)` pattern to a `(Actor)-[ACTED_WITH {actedWithCount}]-(Actor)` where the `actedWithCount` acts as a relationship weight property. This type of projection, where we need to transform multi-hop paths into an aggregated relationship that connects the source and target node, is a commonly occurring pattern in graph analytics.

There are a few other special use cases for Cypher projections too, including merging different node labels and relationship types and defining virtual relationships between nodes based on property conditions or other query logic.


=== Transitioning to Native Projections

While Cypher projections are great for experimenting with these patterns and for small subsets of the graph, we encourage you to transition to native projections as workflows mature, graph projections become larger, and fast performance becomes more important.

For example, with the calculations we made above, we can instead use the following workflow which takes advantage of collapse path in a native projection. This technique does not weight the resulting relationships, the ranking of top actors is not exactly the same but still very similar.

[source, cypher]
----
//set a node label based on recent release and revenue conditions
MATCH (m:Movie)
WHERE m.year >= 1990 AND m.revenue >= 1000000
SET m:RecentBigMovie;

//native projection with reverse relationships
CALL gds.graph.project('proj-native',
  ['Actor','RecentBigMovie'],
  {
  	ACTED_IN:{type:'ACTED_IN'},
    HAS_ACTOR:{type:'ACTED_IN', orientation: 'REVERSE'}
  }
);

//collapse path utility for relationship aggregation - no weight property
CALL gds.alpha.collapsePath.mutate('proj-native',{
    relationshipTypes: ['ACTED_IN', 'HAS_ACTOR'],
    allowSelfLoops: false,
    mutateRelationshipType: 'ACTED_WITH'
});

//count actors that acted with the most other actors in recent high grossing movies and stream the top 15
CALL gds.degree.stream('proj-native', {nodeLabels:['Actor'], relationshipTypes: ['ACTED_WITH']})
YIELD nodeId, score
RETURN gds.util.asNode(nodeId).name AS name, score
ORDER BY score DESC LIMIT 15
----


[options="header"]
|===========================
| name               | score
| Bruce Willis       | 114.0
| Robert De Niro     | 109.0
| Denzel Washington  | 96.0
| Johnny Depp        | 90.0
| Nicolas Cage       | 86.0
| Julianne Moore     | 84.0
| Samuel L. Jackson  | 82.0
| Morgan Freeman     | 81.0
| Ben Affleck        | 81.0
| Brad Pitt          | 79.0
| Sandra Bullock     | 77.0
| George Clooney     | 77.0
| Julia Roberts      | 75.0
| Matt Damon         | 75.0
| Keanu Reeves       | 74.0
|===========================

Here are some general tips for transition workflow from cypher to native projections depending on the initial use case:

. *Filtering with Node Property Conditions*: Add a label to nodes that meet the property condition(s) so you can filter in the native projection
. *Filtering with Relationship Property Conditions:* If possible, add a label to source and target nodes for each relationship that meets the property condition. Otherwise consider adding an Additional Relationship type to your data model to capture the relationships that meet the condition.
. *Aggregating Multi-Hop Paths:*
    .. See if collapse-path can meet your use case needs.  It doesn’t weight relationships, but results can often be very similar to weighted aggregations
    .. For certain types of problems on large projections, similarity and embedding algorithms can be used to approximate the aggregated relationships.

For other complex use cases it will often come back to your data model in the Neo4j database.  Is it possible to adjust your data model so node labels and relationship types better distinguish the data you want to filter on for data science application?  This may involve aggregating certain paths into single relationships, developing more node labels and or relationship types, or other types of transformations.

=== Check your understanding

include::questions/1-graph-projection-using-cypher.adoc[leveloffset=+1]

include::questions/2-cypher-projection-use-cases.adoc[leveloffset=+1]

include::questions/3-cypher-projection-usage.adoc[leveloffset=+1]



[.summary]
== Summary

In this lesson we learned about Cypher projections. What they are, how and when to use them, their pros and cons relative to Native projections, and how to transition between Cypher and native projection strategies.
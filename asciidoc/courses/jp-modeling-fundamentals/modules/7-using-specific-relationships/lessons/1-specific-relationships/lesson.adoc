= 具体的なリレーションシップ
:type: quiz
:order: 1
:updated-at: 2022-04-25 T 21:00:00 Z

[.video]
video::lv4W7030rkU[youtube,width=560,height=315]

[.transcript]
== グラフ内のリレーションシップ

Neo4j はネイティブなグラフデータベースとして、リレーションシップを高速にトラバースするように実装されています。
場合によっては、ノードのプロパティではなく、リレーションシップのタイプに基づいてグラフをクエリする方がよりパフォーマンスが高くなります。

新しいユースケースを見てみよう:

*ユースケース#12: ある俳優がある年に出演した映画は？*

現在のグラフを使って、このクエリを実行することができます:

[source,cypher, role=noplay nocopy]
----
MATCH (p:Actor)-[:ACTED_IN]-(m:Movie)
WHERE p.name = 'Tom Hanks' AND
m.released STARTS WITH '1995'
RETURN m.title AS Movie
----

映画『アポロ13』を返します:

image::images/tom-acted-1995.png[Tom Hanks acted in 1995,width=400,align=center]

トム・ハンクスが 1995 年に 50 本の映画に出演した場合はどうでしょうか。
このクエリは、トム・ハンクスが出演したすべての映画を取得し、_released_ プロパティの値をチェックする必要があります。
Tom Hanksが合計1000本の映画に出演した場合はどうでしょうか？
これらの _Movie_ ノードはすべて評価される必要があります。

そして、もう一つの新しい使用例です。:

*ユースケース#13: ある年に活躍した俳優や監督は？*

現在のグラフでこのクエリを実行することができます:

[source,cypher, role=noplay nocopy]
----
MATCH (p:Person)--(m:Movie)
WHERE  m.released STARTS WITH '1995'
RETURN DISTINCT p.name as `Actor or Director`
----

Tom Hanks と Martin Scorseseを返します:

image::images/actor-or-director-1995.png[Actors or Directors in 1995,width=400,align=center]


このクエリは、結果を返すために、すべての　_Movie_ ノードを取得しなければならないので、パフォーマンスがさらに悪くなります。
グラフに何百万もの _Movie_ が含まれている場合、非常に高価なクエリになることは想像がつくでしょう。

=== リレーションシップを具体化させるリファクタリング

リレーションシップは高速にトラバースでき、グラフ内で大きなスペースを取りません。
前の2つのクエリでは、データモデルがノード間に特化したリレーションシップを持つことでベネフィットを得ることができました。

そこで、例えばACTED_INとDIRECTEDのリレーションシップに加え、Yearの情報を持つリレーションシップを追加していきます。

* ACTED_IN_1992
* ACTED_IN_1993
* ACTED_IN_1995
* DIRECTED_1992
* DIRECTED_1995

最初は、大規模でスケールの大きいムービーグラフのリレーションシップが多いように見えますが、最新の2つの新しいクエリが重要なユースケースであれば、その価値はあります。

インスタンスモデルはこのようになります。:

image::images/instance-specialize-acted-directed.png[Specialized ACTED_IN and DIRECTED relationships,width=400,align=center]

リレーションシップを特化・具体化する場合、既存のクエリがまだ使用する必要があるため、元の一般的なリレーションシップを維持することがほとんどです。

より具体化されたレーションシップを追加する際に、グラフをリファクタリングするコードには、APOCライブラリが使用されています。

次のチャレンジで実行する、グラフ内の_ACTED_IN_リレーションをリファクタリングするコードです。

[source,cypher, role=noplay nocopy]
----
MATCH (n:Actor)-[r:ACTED_IN]->(m:Movie)
CALL apoc.merge.relationship(n,
                              'ACTED_IN_' + left(m.released,4),
                              {},
                              m ) YIELD rel
RETURN COUNT(*) AS `Number of relationships merged`
----

また、グラフ内に動的にリレーションシップを作成できる `apoc.merge.relationship` プロシージャを備えています。
これは、_Movie_ ノードの _released_ プロパティの左端4文字を使用して、リレーションシップの名前を作成します。

リファクタリングの結果、前の2つのクエリは書き直すことができ、大きなグラフに対してより良いパフォーマンスを発揮することは間違いないでしょう:

以下、最初のクエリを書き換えたものです。:

[source,cypher, role=noplay nocopy]
----
MATCH (p:Actor)-[:ACTED_IN_1995]-(m:Movie)
WHERE p.name = 'Tom Hanks'
RETURN m.title AS Movie
----

このクエリでは、特化・具体化されたリレーションシップがトラバースされ、取得されるMovieノードの数は少なくなる。

そして、2つ目のクエリを書き直すと、以下のようになります。:

[source,cypher, role=noplay nocopy]
----
MATCH (p:Person)-[:ACTED_IN_1995|DIRECTED_1995]-()
RETURN p.name as `Actor or Director`
----

このクエリでは、年がリレーションシップ・タイプに含まれているため、_Movie_ ノードを取得する必要はありません。


== 理解度チェック


include::questions/1-why-specialize-relationships.adoc[leveloffset=+1]
include::questions/2-dynamic-relationship-creation.adoc[leveloffset=+1]

[.summary]
== まとめ

このレッスンでは、なぜグラフ内のリレーションシップを特化・具体化すると、クエリパフォーマンスが向上するのかを学びました。
次の課題では、グラフ内の _ACTED_IN_ と _DIRECTED_ のリレーションシップを特化・具体化するリファクタリングを実行します。
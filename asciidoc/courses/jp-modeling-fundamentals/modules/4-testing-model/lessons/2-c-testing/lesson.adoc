= インスタンスモデルによるテスト
:type: challenge
:order: 2
:sandbox: true
:updated-at: 2022-04-25 T 21:00:00 Z

以下は、グラフに表示されるインスタンスモデルです。

image::images/after-challenge2-instance-model.png[Current instance model,width=400,align=center]

この課題では、Cypherクエリを実行することで、グラフに対する各ユースケースをテストします。
クエリの値は、グラフにあることがわかっているデータに固有のものですが、完全に入力されたグラフは、より広い範囲のクエリパラメータを満たすことができると想像できます。

以下の手順で、私たちのアプリケーションの各ユースケースをテストしてください。

== ユースケース#1: 映画に出演したのはだれか

このCypherのコードを実行し、映画「Sleepless in Seattle」を使ってこのユースケースをテストしてください。

[source,cypher]
----
MATCH (p:Person)-[:ACTED_IN]-(m:Movie)
WHERE m.title = 'Sleepless in Seattle'
RETURN p.name AS Actor
----

トム・ハンクス、メグ・ライアンという2人の俳優の名前が返ってくるはずです。

== ユースケース#2: 映画の監督をしたのはだれか

このCypherのコードを実行し、映画「Hoffa」を使ってこのユースケースをテストしてください。

[source,cypher]
----
MATCH (p:Person)-[:DIRECTED]-(m:Movie)
WHERE m.title = 'Hoffa'
RETURN  p.name AS Director
----

ダニー・デビートを返すはずです。

== ユースケース#3: 特定の人が出演した映画は

このCypherのコードを実行して、Tom Hanksという人物を使ってこのユースケースをテストしてください。

[source,cypher]
----
MATCH (p:Person)-[:ACTED_IN]-(m:Movie)
WHERE p.name = 'Tom Hanks'
RETURN m.title AS Movie
----

映画『アポロ13』や『スリープレス・イン・シアトル』を返すはずです。

== ユースケース#4: 何人のユーザーが映画を評価したか

このCypherのコードを実行し、映画「Apollo 13」を使ってこのユースケースをテストしてください。

[source,cypher]
----
MATCH (u:User)-[:RATED]-(m:Movie)
WHERE m.title = 'Apollo 13'
RETURN count(*) AS `Number of reviewers`
----

2を返すはずです。

== ユースケース#5: 映画に出演した最年少の人物は

このCypherのコードを実行し、映画「Hoffa」を使ってこのユースケースをテストしてください。

[source,cypher]
----
MATCH (p:Person)-[:ACTED_IN]-(m:Movie)
WHERE m.title = 'Hoffa'
RETURN  p.name AS Actor, p.born as `Year Born` ORDER BY p.born DESC LIMIT 1
----

ダニー・デビートと誕生年を返すはずです。

== ユースケース#6: 映画の中で、人はどんな役割を担っていたか

このCypherコードを実行して、映画「Sleepless in Seattle」と人物「Meg Ryan」を使って、このユースケースをテストしてください。

[source,cypher]
----
MATCH (p:Person)-[r:ACTED_IN]-(m:Movie)
WHERE m.title = 'Sleepless in Seattle' AND
p.name = 'Meg Ryan'
RETURN  r.role AS Role
----

アニー・リードを返すはずです。

== ユースケース7: imDBによると、ある年に最も高い評価を得た映画は何か

このCypherのコードを実行し、1995年の映画を使ってこのユースケースをテストしてください。

[source,cypher]
----
MATCH (m:Movie)
WHERE m.released STARTS WITH '1995'
RETURN  m.title as Movie, m.imdbRating as Rating ORDER BY m.imdbRating DESC LIMIT 1
----

テストデータには1995年に公開された映画が1つしかありませんが、1995年に公開された別の映画をグラフに追加すれば、このクエリは正しく回答されるでしょう。
この場合、このクエリを完全にテストするために、グラフにさらにデータを追加する必要があります。

Cypherのコードを実行して、別の _Movie_ ノードとその監督をグラフに追加してください。

[source,cypher]
----
MERGE (casino:Movie {title: 'Casino', tmdbId: 524, released: '1995-11-22', imdbRating: 8.2, genres: ['Drama','Crime']})
MERGE (martin:Person {name: 'Martin Scorsese', tmdbId: 1032})
MERGE (martin)-[:DIRECTED]->(casino)
----

上記のクエリを再実行してユースケースをテストし、異なる結果がかえってくることを確認できるでしょう。

このグラフの新しいインスタンスモデルが出来上がりました。

image::images/testing-addition-instance-model.png[Testing addition,width=400,align=center]

== ユースケース#8: 特定の俳優が出演したドラマ映画とは

このCypherのコードを実行して、Tom Hanksという人物を使ってこのユースケースをテストしてください。

[source,cypher]
----
MATCH (p:Person)-[:ACTED_IN]-(m:Movie)
WHERE p.name = 'Tom Hanks' AND
'Drama' IN m.genres
RETURN m.title AS Movie
----

このクエリで「Drama」を「Comedy」に変更した場合、異なる結果が返されます。

== ユースケース#9: 映画の評価に5をつけたのはどのユーザーか

このCypherのコードを実行し、映画「Apollo 13」を使ってこのユースケースをテストしてください。

[source,cypher]
----
MATCH (u:User)-[r:RATED]-(m:Movie)
WHERE m.title = 'Apollo 13' AND
r.rating = 5
RETURN u.name as Reviewer
----

サンディ・ジョーンズを返すはずです。

以下のクエリは、グラフ内のすべてを返します。このクエリを実行し、インスタンスモデルについて以下の質問に答えてください。

[source,cypher]
----
MATCH (n) RETURN n
----


== ノード数・リレーションシップ数

サンドボックスウィンドウを使って、ここまでのグラフについて以下の質問に答えてください。

include::./questions/1-person-nodes.adoc[leveloffset=+1]

include::./questions/2-movie-nodes.adoc[leveloffset=+1]

include::./questions/3-user-nodes.adoc[leveloffset=+1]

include::./questions/4-acted-in-relationships.adoc[leveloffset=+1]

include::./questions/5-directed-relationships.adoc[leveloffset=+1]

include::./questions/6-rated-relationships.adoc[leveloffset=+1]


[.summary]
== まとめ

この課題では、インスタンスモデルに対してユースケースをテストしました。

次のモジュールでは、データモデルをリファクタリングする理由と方法について学びます。

= Deleting Data
:type: video
:order: 9

[.video]
video::qNY4ltwe8po[youtube,width=560,height=315]

// new video is: https://youtu.be/qNY4ltwe8po
////
2-Writing-DeletingData

https://docs.google.com/document/d/1IgJFtqQ0epVu3ywM7cNgbe6nuTxpz-TVTarGV74-pqs/edit?usp=sharing

////
[.transcript]
== Deleting data
In a Neo4j database you can delete:

* nodes
* relationships
* properties
* labels

To delete any data in the database, you must first retrieve it, then you can delete it.
You have already learned how to remove or delete properties from nodes or relationships.

=== Deleting a node

Suppose you have created a _Person_ node for _Jane Doe_.

Run this Cypher code to create the node:

[source,cypher]
----
MERGE (p:Person {name: 'Jane Doe'})
----

You delete this node as follows where you first retrieve the node. Then with a reference to the node you can delete it.

[source,cypher]
----
MATCH (p:Person {name: 'Jane Doe'})
DELETE p
----

=== Deleting a relationship

Suppose we had our _Jane Doe_ node again where she was added as an actor in the movie, _The Matrix_.
Run this code to create the node and the relationship.

[source,cypher]
----
MATCH (m:Movie {title: 'The Matrix'})
MERGE (p:Person {name: 'Jane Doe'})
MERGE (p)-[:ACTED_IN]->(m)
RETURN p, m
----

This code creates one node and the relationship from _Jane Doe_ to _The Matrix_.

To leave the _Jane Doe_ node in the graph, but remove the relationship we retrieve the relationship and delete it.

[source,cypher]
----
MATCH (p:Person {name: 'Jane Doe'})-[r:ACTED_IN]->(m:Movie {title: 'The Matrix'})
DELETE r
RETURN p, m
----

Run this Cypher code that recreates the relationship:

[source,cypher]
----
MATCH (p:Person {name: 'Jane Doe'})
MATCH (m:Movie {title: 'The Matrix'})
MERGE (p)-[:ACTED_IN]->(m)
RETURN p, m
----

If we attempt to delete the _Jane Doe_ node, we will receive an error because it has relationships in the graph.

Try running this Cypher code:

[source,cypher]
----
MATCH (p:Person {name: 'Jane Doe'})
DELETE p
----

You should receive an error. Neo4j prevents orphaned relationships in the graph.


=== Deleting a node and its relationships

Neo4j provides a feature where you cannot delete a node if it has incoming or outgoing relationships.
This prevents the graph from having orphaned relationships.

Run this Cypher code:

[source,cypher]
----
MATCH (p:Person {name: 'Jane Doe'})
DETACH DELETE p
----

This code deletes the relationship and the _Person_ node.

You can also delete all nodes and relationships in a database with this code.

[NOTE]
Do NOT delete all nodes and relationships in the graph as you need them for the next challenge!

[source,cypher,role=nocopy noplay]
----
MATCH (n)
DETACH DELETE n
----

[IMPORTANT]
You should only do this on relatively small databases as trying to do this on a large database will exhaust memory.

=== Deleting labels

A best practice is to have at least one label for a node.

Run this Cypher code to create this Person node in the graph:

[source,cypher]
----
MERGE (p:Person {name: 'Jane Doe'})
RETURN p
----

Next, run this code to add a new label to this node:

[source,cypher]
----
MATCH (p:Person {name: 'Jane Doe'})
SET p:Developer
RETURN p
----

To remove the newly-added label, Developer, you use the `REMOVE` clause. Run this code:

[source,cypher]
----
MATCH (p:Person {name: 'Jane Doe'})
REMOVE p:Developer
RETURN p
----

The Jane Doe node has two labels, Person and Developer. You can use a `MATCH` to find that node.
Note that you could have specified `MATCH (p:Developer {name: 'Jane Doe'})` or `MATCH (p:Person:Developer {name: 'Jane Doe'})` to find the same node.
Once we have a reference to that node, we can remove the label with the `REMOVE` clause.

And finally, delete the Jane Doe node by running this code:

[source,cypher]
----
MATCH (p:Person {name: 'Jane Doe'})
DETACH DELETE p
----

Note that `DELETE p` would also work in this case since we have not created any relationships.

[TIP]
.What labels exist in the graph?

You can find out what labels exist in the graph with this code:

[source,cypher]
----
CALL db.labels()
----

== Check your understanding

include::questions/1-complete-code.adoc[leveloffset=+1]

include::questions/2-delete-all.adoc[leveloffset=+1]

[.summary]
== Summary

In this lesson, you learned how to delete nodes and relationships from the graph.
In the next challenge, you will demonstrate that you can delete a node from the graph.

= Updating Properties
:type: video
:order: 5
:slides: true

[.video]
video::TUubik76iZ4[youtube,width=560,height=315]



//https://youtu.be/TUubik76iZ4


////

2-Writing-UpdatingProperties

https://docs.google.com/document/d/1TwXJ2vHQRlw-vQkg_DWduXNZDIkgxVJBSjHeYZcUerQ/edit?usp=sharing

////


[.transcript]
[.slide.discrete]
== Updating properties

[NOTE]
.Video Transcript Correction
The actor's name for Michael Caine is misspelled in the video. It has been corrected in the transcript.remove properties from nodes and relationships.

In this lesson you will learn how to write Cypher code to update properties of nodes and relationships.

[.slide]
== Adding properties for a node or relationship

There are two ways that you can set a property for a node or relationship.

=== 1. Inline as part of the `MERGE` clause

You have already seen how to create the primary key property for a node.
You can also set a property for a relationship inline as follows:


[source,cypher]
----
MATCH (p:Person {name: 'Michael Caine'})
MERGE (m:Movie {title: 'Batman Begins'})
MERGE (p)-[:ACTED_IN {roles: ['Alfred Penny']}]->(m)
RETURN p,m
----

[.slide]
== Inline Property Setting Explained

* In this code,  the actor, _Michael Caine_ exists but the movie, _Batman Begins_ does not.
* We find the _Person_ node and we create the _Movie_ node.
* Then, we create the _ACTED_IN_ relationship between the _Michael Caine_ node and the newly-created _Batman Begins_ node. And we set the _roles_ property for this relationship to an array of values - containing one value, _Alfred Penny_.
* Notice that for inline property setting, we use the JSON-style of adding the property key/value pairs in braces `{ .. }`, just like we did when we specified the property for the node.

[.slide]
== Using the `SET` keyword

We also have the option to use the `SET` keyword for setting a property value.
In the context of particular `MERGE`  or `MATCH` clause where you have defined a variable to reference the node or relationship, you can set property values.

[source,cypher]
----
MATCH (p:Person)-[r:ACTED_IN]->(m:Movie)
WHERE p.name = 'Michael Caine' AND m.title = 'The Dark Knight'
SET r.roles = ['Alfred Penny']
RETURN p, r, m
----

[.slide]
== Setting multiple properties

If you need to set multiple properties, you separate them with a comma (,). For example:

[source,cypher]
----
MATCH (p:Person)-[r:ACTED_IN]->(m:Movie)
WHERE p.name = 'Michael Caine' AND m.title = 'The Dark Knight'
SET r.roles = ['Alfred Penny'], m.released = 2008
RETURN p, r, m
----

[.slide]
== Updating properties

If you have a reference to a node or relationship, you can also use `SET` to modify the property.
For example, if we wanted to modify _Michael Caine's_ role to be something different, we could do the following:

[source,cypher]
----
MATCH (p:Person)-[r:ACTED_IN]->(m:Movie)
WHERE p.name = 'Michael Caine' AND m.title = 'The Dark Knight'
SET r.roles = ['Mr. Alfred Penny']
RETURN p, r, m
----

[.slide]
== Removing properties

You can remove or delete a property from a node or relationship by using the `REMOVE` keyword, or setting the property to `null`.

Here we remove the roles property of this relationship:

[source,cypher]
----
MATCH (p:Person)-[r:ACTED_IN]->(m:Movie)
WHERE p.name = 'Michael Caine' AND m.title = 'The Dark Knight'
REMOVE r.roles
RETURN p, r, m
----

[.slide]
== Alternative: Setting to null

Here we remove the _born_ property from an actor by setting it to `null`:

[source,cypher]
----
MATCH (p:Person)
WHERE p.name = 'Gene Hackman'
SET p.born = null
RETURN p
----

[IMPORTANT]
.Protecting Primary Key Properties
You should never remove the property that is used as the primary key for a node.

[.quiz]
== Check your understanding

include::questions/1-set-keyword.adoc[leveloffset=+1]

include::questions/2-add-property.adoc[leveloffset=+1]

include::questions/3-complete-code.adoc[leveloffset=+1]

[.summary]
== Summary

In this lesson, you learned how to create, update and delete properties for nodes and relationships.
In the next challenge, you will add properties to a node.

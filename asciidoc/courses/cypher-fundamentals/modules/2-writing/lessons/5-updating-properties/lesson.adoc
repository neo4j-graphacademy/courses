= Updating Properties
:type: quiz
:order: 5

[.video]
video::TUubik76iZ4[youtube,width=560,height=315]


//https://youtu.be/TUubik76iZ4


////

2-Writing-UpdatingProperties

https://docs.google.com/document/d/1TwXJ2vHQRlw-vQkg_DWduXNZDIkgxVJBSjHeYZcUerQ/edit?usp=sharing

////


[.transcript]
== Updating properties

Thus far, you have learned how to create nodes with `MERGE` where you specify the primary key property for the node.
You can add, modify, or remove properties from nodes and relationships.

In this lesson you will learn how to write Cypher code to update properties of nodes and relationships.

=== Adding properties for a node or relationship

There are two ways that you can set a property for a node or relationship.

==== 1. Inline as part of the `MERGE` clause

You have already seen how to create the primary key property for a node.
You can also set a property for a relationship inline as follows:


[source,cypher]
----
MERGE (p:Person {name: 'Michael Cain'})
MERGE (m:Movie {title: 'Batman Begins'})
MERGE (p)-[:ACTED_IN {roles: ['Alfred Penny']}]->(m)
RETURN p,m
----

In this code,  the actor, _Michael Cain_ exists but the movie, _Batman Begins_ does not. We find the _Person_ node and we create the _Movie_ node.
Then, we create the _ACTED_IN_ relationship between the _Michael Cain_ node and the newly-created _Batman Begins_ node. And we set the _roles_ property for this relationship to an array of values - containing one value, _Alfred Penny_.
Notice that for inline property setting, we use the JSON-style of adding the property key/value pairs in braces `{ .. }`, just like we did when we specified the property for the node.

==== 2. Using the `SET` keyword for a reference to a node or relationship

We also have the option to use the `SET` keyword for setting a property value.
In the context of particular `MERGE`  or `MATCH` clause where you have defined a variable to reference the node or relationship, you can set property values.

[source,cypher]
----
MATCH (p:Person)-[r:ACTED_IN]->(m:Movie)
WHERE p.name = 'Michael Cain' AND m.title = 'The Dark Knight'
SET r.roles = ['Alfred Penny']
RETURN p, r, m
----

=== Updating properties

If you have a reference to a node or relationship, you can also use `SET` to modify the property.
For example, if we wanted to modify _Michael Cain's_ role to be something different, we could do the following:

[source,cypher]
----
MATCH (p:Person)-[r:ACTED_IN]->(m:Movie)
WHERE p.name = 'Michael Cain' AND m.title = 'The Dark Knight'
SET r.roles = ['Mr. Alfred Penny']
RETURN p, r, m
----

=== Removing properties

You can remove a property from a node or relationship by using the `REMOVE` keyword, or setting the property to `null`.

Here we remove the roles property of this relationship:

[source,cypher]
----
MATCH (p:Person)-[r:ACTED_IN]->(m:Movie)
WHERE p.name = 'Michael Cain' AND m.title = 'The Dark Knight'
REMOVE r.roles
RETURN p, r, m
----

Here we remove the _born_ property from an actor:

[source,cypher]
----
MATCH (p:Person)
WHERE p.name = 'Gene Hackman'
SET p.born = null
RETURN p
----

[IMPORTANT]
You should never remove the property that is used as the primary key for a node.

== Check your understanding

include::questions/1-set-keyword.adoc[leveloffset=+1]

include::questions/2-add-property.adoc[leveloffset=+1]

include::questions/3-complete-code.adoc[leveloffset=+1]

[.summary]
== Summary

In this lesson, you learned how to create, update and delete properties for nodes and relationships.
In the next challenge, you will add properties to a node.

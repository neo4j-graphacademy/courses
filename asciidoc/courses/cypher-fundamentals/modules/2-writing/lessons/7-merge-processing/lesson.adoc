= Merge Processing
:type: video
:order: 7

[.video]
video::3zHH1ei5eCw[youtube,width=560,height=315]

[NOTE]
The actor's name for Michael Caine is misspelled in the video. It has been corrected in the transcript.


//https://youtu.be/3zHH1ei5eCw


////

2-Writing-MergeProcessing

https://docs.google.com/document/d/1AFfffANGSB6-YpfjFJ6qRkxONnsw-PMrhFp1dz0qtLo/edit?usp=sharing

////
[.transcript]
== Merge processing
You have learned that you can use `MERGE` to create nodes and relationships in the graph.
`MERGE` operations work by first trying to find a pattern in the graph.
If the pattern is found then the data already exists and is not created.
If the pattern is not found, then the data can be created.

=== Customizing `MERGE` behavior

You can also specify behavior at runtime that enables you to set properties when the node is created or when the node is found.
We can use the `ON CREATE SET` or `ON MATCH SET` conditions, or the `SET` keywords to set any additional properties.

In this example, if the _Person_ node for _McKenna Grace_ does not exist, it is created and the _createdAt_ property is set.
If the node is found, then the _updatedAt_ property is set.
In both cases, the _born_ property is set.

Run this Cypher code at least 2 times to observe what properties are set. You can see the properties in table view.

[source,cypher]
----
// Find or create a person with this name
MERGE (p:Person {name: 'McKenna Grace'})

// Only set the `createdAt` property if the node is created during this query
ON CREATE SET p.createdAt = datetime()

// Only set the `updatedAt` property if the node was created previously
ON MATCH SET p.updatedAt = datetime()

// Set the `born` property regardless
SET p.born = 2006

RETURN p
----

If you want to set multiple properties for an `ON CREATE SET` or `ON MATCH SET` clause, you separate them by commas.
For example:

`ON CREATE SET
  m.released = 2020, m.tagline = `A great ride!'`

=== Merging  with relationships

You can use `MERGE` to create nodes or relationships:

Run this Cypher code:

[source,cypher]
----
// Find or create a person with this name
MERGE (p:Person {name: 'Michael Caine'})

// Find or create a movie with this title
MERGE (m:Movie {title: 'The Cider House Rules'})

// Find or create a relationship between the two nodes
MERGE (p)-[:ACTED_IN]->(m)
----

Another way your can create these nodes and relationship is as follows:

[source,cypher]
----
MERGE (p:Person {name: 'Michael Caine'})-[:ACTED_IN]->(m:Movie {title: 'The Cider House Rules'})
RETURN p, m
----

Here is what happens in the query processor:

1. Neo4j will attempt to find a _Person_ node with the name _Michael Caine_.
2. If it does not exist, it creates the node.
3. Then, it will attempt to expand the _ACTED_IN_ relationships in the graph for this node.
4. If there are any _ACTED_IN_ relationships from this node, it looks for a Movie with the title 'The Cider House Rules'.
5. If there is no node for the Movie, it creates the node.
6. If there is no relationship between the two nodes, it then creates the _ACTED_IN_ relationship between them.

[.quiz]
== Check your understanding

include::questions/1-what-merge-does.adoc[leveloffset=+2]]

include::questions/2-complete-code.adoc[leveloffset=+2]]

[.summary]
== Summary

In this lesson, you learned how you can perform additional setting of properties when you create or find a node in the graph.
In the next challenge, you will demonstrate how to add or update properties when a node is created or found in the graph.


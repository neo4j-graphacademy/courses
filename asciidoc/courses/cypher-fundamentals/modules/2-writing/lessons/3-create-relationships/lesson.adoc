= Creating Relationships
:type: quiz
:order: 3

[.video]
video::9npLX5us1DU[youtube,width=560,height=315]

[NOTE]
The actor's name for Michael Caine is misspelled in the video. It has been corrected in the transcript.


//https://youtu.be/9npLX5us1DU


////

2-Writing-CreatingRelationships

https://docs.google.com/document/d/1sZRzA7uqqkjcYee_vQM-KRBlILhkyZvEQa_PAX4CdHg/edit?usp=sharing

////


[.transcript]
== Creating a relationship between two nodes

In this lesson you will learn how to write Cypher clauses to create relationships between existing nodes in the graph.

Just like you can use `MERGE` to create nodes in the graph, you use `MERGE` to create relationships between two nodes.
First you must have references to the two nodes you will be creating the relationship for.
When you create a relationship between two nodes, it must have:

* Type
* Direction

For example, if the _Person_ and _Movie_ nodes both already exist, we can find them using a `MATCH` clause before creating the relationship between them.

[source,cypher]
----
MATCH (p:Person {name: 'Michael Caine'})
MATCH (m:Movie {title: 'The Dark Knight'})
MERGE (p)-[:ACTED_IN]->(m)
----

Here we find the two nodes that we want to create the relationship between.
Then we use the reference to the found nodes to create the _ACTED_IN_ relationship.

We can confirm that this relationship exists as follows:

[source,cypher]
----
MATCH (p:Person {name: 'Michael Caine'})-[:ACTED_IN]-(m:Movie {title: 'The Dark Knight'})
RETURN p, m
----

By default, in Neo4j Browser, the visualization connects nodes that have relationships between them.

Notice also that you need not specify direction in the `MATCH` pattern since the query engine will look for all nodes that are connected, regardless of the direction of the relationship.

For example, if we specified this relationship pattern:

[source,cypher]
----
MATCH (p:Person {name: 'Michael Caine'})<-[:ACTED_IN]-(m:Movie {title: 'The Dark Knight'})
RETURN p, m
----

This query returns no nodes since there are no nodes with the _ACTED_IN_ relationship to _Person_ nodes in the graph.

=== Creating nodes and relationships using multiple clauses

We can also chain multiple `MERGE` clauses together within a single Cypher code block.

[source,cypher]
----
MERGE (p:Person {name: 'Chadwick Boseman'})
MERGE (m:Movie {title: 'Black Panther'})
MERGE (p)-[:ACTED_IN]-(m)
----

This code creates two nodes and a relationship between them.
Because we have specified the variables _p_ and _m_, we can use them in the code to create the relationship between the two nodes.

[TIP]
====
Note that in this `MERGE` clause where we create the relationships, we did not specify the direction of the relationship.
By default, if you do not specify the direction when you create the relationship, it will always be assumed left-to-right.
====

We can confirm that this relationship exists as follows:

[source,cypher]
----
MATCH (p:Person {name: 'Chadwick Boseman'})-[:ACTED_IN]-(m:Movie {title: 'Black Panther'})
RETURN p, m
----

=== Using `MERGE` to create nodes and a relationship in single clause

What `MERGE` does is create the node or relationship if it does not exist in the graph.

This code successfully creates the nodes and relationship:

[source,cypher]
----
MERGE (p:Person {name: 'Emily Blunt'})-[:ACTED_IN]->(m:Movie {title: 'A Quiet Place'})
RETURN p, m
----

You can execute this Cypher code multiple times and it will not create any new nodes or relationships.


== Check your understanding

include::questions/1-merge-keyword.adoc[leveloffset=+1]

include::questions/2-type-direction-for-relationship.adoc[leveloffset=+1]

include::questions/3-complete-code.adoc[leveloffset=+1]

[.summary]
== Summary

In this lesson, you learned how to create relationships in the graph.
In the next challenge, you will demonstrate that you can create a relationship between nodes.


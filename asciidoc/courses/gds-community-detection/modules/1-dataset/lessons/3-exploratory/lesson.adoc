= Exploratory Analysis
:type: quiz
:sandbox: true
:order: 3

[.transcript]
Now that we have some data in our Neo4j instance, lets perform exploratory analysis to better understand the dataset.

To do this, we will use the `apoc.agg.statistics` function
The APOC plugin offers a  function that allows us to easily evaluate a distribution.

== Survey demographics

We will begin by examining the population information.
First, let's take a look at the age distribution.

.Age Distribution
[source,cypher]
----
MATCH (p:Person)
RETURN apoc.agg.statistics(p.Age);
----

.Age Distribution Result
[%header,cols=1]
|===
| apoc.agg.statistics(p.Age)
|
  `{
    "total": 2006,
    "min": 15.0,
    "minNonZero": 15.0,
    "max": 30.0,
    "mean": 20.433759269067796,
    "0.5": 20.00006103515625,
    "0.99": 29.00006103515625,
    "0.75": 22.00006103515625,
    "0.9": 24.00006103515625,
    "0.95": 27.00006103515625,
    "stdev": 2.8274308440353533
  }`
|===

Both the average and the median age of respondents is 20.
It looks like the vast majority of the population is between 18 to 22 years old.

Next, let's take a look a look at gender distribution.

.Gender Count
[source,cypher]
----
MATCH (p:Person)
RETURN p.Gender AS gender,
       count(*) AS count;
----

.Gender Count Results
[%header,cols="1,1"]
|===
| gender
| count
| "female"
| 593
| "male"
| 411
| null
| 6
|===

There are roughly 60% of females and 40% of males.
We can also observe six missing values of the gender information.

== Most popular preferences

We can now start to examine the survey responses.
We will begin by examining which interests or preferences the respondents most strongly agree with.

.Demographic Features
[source,cypher]
----
WITH ["Height", "Weight", "Age", "Number of siblings"] AS demographicFeatures
MATCH (p:Person)
UNWIND keys(p) as key
WITH p, key
WHERE toInteger(p[key]) IS NOT NULL
  AND NOT key IN demographicFeatures
WITH key, avg(p[key]) AS average
RETURN key, average
ORDER BY average DESC
LIMIT 5;
----

.Demographic Features Results
[%header,cols="1,1"]
|===
| key
| average
| "Music"
| 4.731876861966248
| "Movies"
| 4.61354581673306
| "Fun with friends"
| 4.557654075546721
| "Comedy"
| 4.494538232373391
| "Internet"
| 4.175944333996029
|===


According to the survey, most young people like to listen to music, watch movies, and have fun with friends.

== Age Correlation

We can see which answers correlate with the age of the respondants using the link:https://neo4j.com/docs/graph-data-science/current/algorithms/similarity-functions/[`gds.similarity.pearson` function^].  The `gds.similarity.pearson` function used to calculate the Pearson similarity coefficient between pairs of nodes based on specified properties.

The Pearson similarity coefficient, or Pearson's correlation coefficient, is a measure of the linear correlation between two sets of data. It has a value between +1 and -1, where +1 indicates a total positive linear correlation, 0 indicates no linear correlation, and -1 indicates a total negative linear correlation.

The Cypher statement below finds all numerical properties, creates two collections that correspond to the age of the participant (`key1`) and their response (`key2`) and uses the `gds.similarity.pearson` function to compute the coefficient.


[source,cypher]
----
MATCH (p:Person)
UNWIND [x in keys(p) WHERE toInteger(p[x]) IS NOT NULL
            AND x <> "Age" | x] AS key1
WITH p,key1,"Age" AS key2
WITH key1, key2, collect(coalesce(p[key1], 3)) as vector1,
                 collect(coalesce(p[key2], 3)) as vector2
RETURN key1,key2, gds.similarity.pearson(vector1, vector2) as pearsonCorrelation
ORDER BY pearsonCorrelation DESC
LIMIT 5;
----
// TODO: Results

The most correlated attribute with age is weight, followed by elections, preference for watching thrillers, and prioritizing workload.

== Check your understanding
include::questions/1-statistics.adoc[leveloffset=+2]

include::questions/2-pearson.adoc[leveloffset=+2]

[.summary]
== Summary
In this lesson, we took a closer look at the dataset, how responses are distributed, and identified correlations between age and other survey responses.

In the next lesson, you must use this knowledge to answer a question about the data.

= Filtering results
:type: lesson
:order: 2
:sandbox: true

== WHERE

You can filter the results of a query using the `WHERE` clause.

[source, cypher]
.Tom Hanks movies released in 2013
----
MATCH (p:Person)-[:ACTED_IN]->(m:Movie)
WHERE p.name = "Tom Hanks"
AND m.year = 2013
RETURN m.title
----

== Comparison operators

Cypher supports operators in the `WHERE` clause, including:

- Comparison operators:
+
`=`, `<>` - equal, not equal
+
`<`, `>` - less than, greater than
+
`<=`, `>=` - less than or equal to, greater than or equal to

- Strings - `CONTAINS`, `STARTS WITH`, `ENDS WITH`
- Binary operators - `AND`, `OR`, `XOR`, `NOT`
- Null values - `IS NULL`, `IS NOT NULL`
- List membership - `IN`

== Testing ranges

Using comparison and binary operators, you can test for ranges:

[source, cypher]
.Tom Hanks movies released between 2005 and 2010
----
MATCH (p:Person)-[:ACTED_IN]->(m:Movie)
WHERE p.name = "Tom Hanks"
  AND 2005 <= m.year <= 2010
RETURN m.title, m.released
----

== String operators

There are specific operators for strings, allowing you to filter based on the contents of a string.

[source, cypher]
.Movies staring someone with "Robert" in their name
----
MATCH (p:Person)-[:ACTED_IN]->(m:Movie)
WHERE p.name CONTAINS "Robert"
RETURN p.name, m.title
----

You can also use `STARTS WITH` and `ENDS WITH` to filter based on the start or end of a string.

[source, cypher]
.Movies staring someone called "Robert"
----
MATCH (p:Person)-[:ACTED_IN]->(m:Movie)
WHERE p.name STARTS WITH "Robert"
RETURN p.name, m.title
----

== Null values

Properties that are not set are `null` in Neo4j.
You can use the `IS NULL` and `IS NOT NULL` operators to filter based on null values.

This query find people who are over 100 years old by test if the `died` property is NULL and the year they were `born`.

[source, cypher]
.People over 100 years old
----
MATCH (p:Person)
WHERE p.died IS NULL
AND p.born.year <= 1925
RETURN p.name, p.born, p.died
----

== Lists

You can use the `IN` operator to test if a value is in a list.
The `Movie` nodes contains a `languages` property, which is a list of languages spoken in the movie.

[source, cypher]
.Movies in French
----
MATCH (m:Movie)
WHERE "French" IN m.languages
RETURN m.title
----

== Challenges

Complete the following queries to find the data in the graph.

. Find the directors of `Horror` movies released in the year `2000`.
+ 
[source, cypher]
----
MATCH (d:Director)-[:DIRECTED]->(m:Movie)-[:IN_GENRE]->(g:Genre)
WHERE ?????? AND ??????
RETURN d.name
----
. Find all movies that do *not* have a `tmdbId` property (i.e. it is null).
+
[source, cypher]
----
MATCH (m:Movie)
WHERE ???????
RETURN m.title
----
. Find people `born` in the 1950’s (1950 - 1959).
+
[source, cypher]
----
MATCH (p:Person)
WHERE p.born.year ??????
RETURN p.name, p.born
----

[%collapsible]
.Click to reveal the answers
====
. Find the directors of `Horror` movies released in the `year` `2000`.
+ 
[source, cypher]
----
MATCH (d:Director)-[:DIRECTED]->(m:Movie)-[:IN_GENRE]->(g:Genre)
WHERE m.year = 2000 AND g.name = "Horror"
RETURN d.name
----
. Find all movies that do *not* have a `tmdbId` property (i.e. it is null).
+
[source, cypher]
----
MATCH (m:Movie)
WHERE m.tmdbId IS NULL
RETURN m.title
----
. Find people `born` in the 1950’s (1950 - 1959).
+
[source, cypher]
----
MATCH (p:Person)
WHERE 1950 <= p.born.year <= 1959
RETURN p.name, p.born
----
====


read::Continue[]

[.summary]
== Summary

In this lesson, you learned how to filter results using the `WHERE` clause.

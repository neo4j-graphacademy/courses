= Matching Data
:type: lesson
:order: 1
:sandbox: true

== Patterns
You find data in Neo4j by matching patterns using Cypher. 

Review this pattern that represents _action movies_:

    (m:Movie)-[:IN_GENRE]->(g:Genre {name: "Action"})

It finds:

. All nodes with a label of `Movie`, 
. that have an _outgoing_ `IN_GENRE` relationship
. to a node with a label of `:Genre`
. that has property of `name` with a value of `Action`:

== MATCH

You can find this data using the `MATCH` clause:

[source, cypher]
----
MATCH (m:Movie)-[:IN_GENRE]->(g:Genre {name: "Action"})
RETURN m.title, g.name
----


Patterns can contain multiple nodes and relationships.
For example, finding all the actors in action movies:

[source, cypher]
----
MATCH (actor:Person)-[:ACTED_IN]->(m:Movie)-[:IN_GENRE]->(g:Genre {name: "Action"})
RETURN actor.name, m.title, g.name
----

You can use multiple MATCH clases to find data distributed across the graph.

For example also finding the director of the movies:

== Multiple patterns

[source, cypher]
----
MATCH (actor:Person)-[:ACTED_IN]->(m:Movie)-[:IN_GENRE]->(g:Genre {name: "Action"})
MATCH (m)<-[:DIRECTED]-(director:Person)
RETURN actor.name, director.name, m.title, g.name
----

== Optional MATCH

Sometimes you want to find data that may or may not exist in the graph.

For example, you may want to find all the movies for a certain genre and their ratings, but some movies may not have a rating:

This query uses `OPTIONAL` to "Film-Noir" movies and the users who rated them.
Any movie that does not have a rating will return `null` for the user's name and rating.

[source, cypher]
----
MATCH (m:Movie)-[:IN_GENRE]->(g:Genre {name: "Film-Noir"})
OPTIONAL MATCH (m)<-[r:RATED]-(u:User)
RETURN m.title, u.name, r.rating
----

== Distinct Rows

When you use `RETURN` to return data, Neo4j will return all the rows that match the pattern.
If you want to return only unique rows, you can use the `DISTINCT` keyword.

This query returns the names of all the actors who have acted with Tom Hanks.

[source, cypher]
----
MATCH (p:Person {name: "Tom Hanks"})-[:ACTED_IN]->(:Movie)<-[:ACTED_IN]-(p2:Person)
RETURN DISTINCT p.name, p2.name
----

The `DISTINCT` keyword ensures that each actor is only returned once, even if they have acted in multiple movies with Tom Hanks.

== Alias

You can use the `AS` keyword to give a name to the data you are returning.

[source, cypher]
----
MATCH (m:Movie)-[:IN_GENRE]->(g:Genre)
RETURN m.title AS movieTitle, g.name AS genre
----

== Challenges

Complete the following queries to find the data in the graph.

. Find a movie you like:
+
[source, cypher]
----
MATCH (m:??????) RETURN m.title AS movieTitle
----

. Use the `ACTED_IN` relationship to find who acted in that movie.
[source, cypher]
----
MATCH (m:Movie {title: "???"})<-[:??????]-(p:Person)
RETURN p.name AS actor
----

. Use the DIRECTED relationship to find who directed the movie
[source, cypher]
----
MATCH (m:Movie {title: "???"})<-[:??????]-(p:Person)
RETURN p.name AS director
----

[%collapsible]
.Click to reveal the answers
====
. Find a movie you like:
+
[source, cypher]
----
MATCH (m:Movie) RETURN m.title AS movieTitle
----

. Use the `ACTED_IN` relationship to find who acted in that movie.
[source, cypher]
----
MATCH (m:Movie {title: "Jumanji"})<-[:ACTED_IN]-(p:Person)
RETURN p.name AS actor
----

. Use the DIRECTED relationship to find who directed the movie
[source, cypher]
----
MATCH (m:Movie {title: "Jumanji"})<-[:DIRECTED]-(p:Person)
RETURN p.name AS director
----
====


read::Continue[]

[.summary]
== Summary

In this lesson, you learned how to match data in the graph using patterns.

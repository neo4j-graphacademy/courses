= Parameters in Cypher
:type: video
:order: 1
:sandbox: true

[.video]
video::lKQ2k73tBbs[youtube,width=560,height=315]

//https://youtu.be/lKQ2k73tBbs

[.transcript]
== Parameters

As you test your Cypher statements, you will use a variety literal values to ensure that your Cypher queries are correct.
But you don't want to change the Cypher statement every time you test.
In fact, any change to a Cypher statement requires a recompilation of the Cypher code which is expensive.
You create Cypher statements that will not change, except for the substitution of placeholders (parameters) in the query.
A best practice is to parameterize values in your Cypher statements.

=== Using Cypher parameters

In your Cypher statements, a parameter name begins with the `$` symbol.

Here is an example where we have parameterized the query:

// reco db change impact
// video will change as results will look different


[source,cypher]
----
MATCH (p:Person)-[:ACTED_IN]->(m:Movie)
WHERE p.name = $actorName
RETURN m.released AS releaseDate,
m.title AS title
ORDER BY m.released DESC
----

[NOTE]
--
You will not  be able to run this code yet in Neo4j Browser because you have not yet set a value for the parameter.
--

At runtime, if the parameter `$actorName` has a value, it will be used in the Cypher statement when it runs in the graph engine.

=== Setting a parameter

You can set values for Cypher parameters that will be in effect during your Neo4j Browser session.

You can set the value of a single parameter in the query editor pane as shown in this example where the value _Tom Hanks_ is set for the parameter `actorName`:

[source,cypher]
----
:param actorName: 'Tom Hanks'

----

//
// You can even specify a Cypher expression to the right of `+=>+` to set the value of the parameter.

After you have set the parameter, you can then successfully run the Cypher code:

// reco db change impact
// video will change as results will look different


[source,cypher]
----
MATCH (p:Person)-[:ACTED_IN]->(m:Movie)
WHERE p.name = $actorName
RETURN m.released AS releaseDate,
m.title AS title
ORDER BY m.released DESC
----

=== Setting Integers

Special consideration should be made when setting **integer** values in a Neo4j Browser session.
Due to a discrepancy between integers in JavaScript and the Neo4j type system, any integers are converted to floating point values when the parameter is set.  This is designed to avoid any data loss on large numbers.

For example, if you run the following code to set the `number` parameter using colon (`:`) operator, the number will be converted from `10` to `10.0`.


.Numbers and Colons
[source,cypher]
----
:param number: 10
----

The Browser will output the following result, with number cast as a float.

.Browser Output
[source,json]
----
{
  "number": 10.0
}
----


Instead, to force the number to be an integer, you can use the `=>` operator.


.Correctly setting Integers
[source,cypher]
----
:param number=> 10
----

The Browser will output the following result, with the number cast as an integer.

.Browser Output
[source,json]
----
{
  "number": 10
}
----

You can learn more about the difference between JavaScript integers and Neo4j Integers in the link:/courses/app-nodejs/2-interacting/3-type-system/[Building Neo4j Applications with Node.js course^].


=== Setting multiple parameters

You can also use the JSON-style syntax to set _all_ of the parameters in your Neo4j Browser session.
The values you can specify in this object are numbers, strings, and booleans.
In this example we set two parameters for our session:

[source,cypher]
----
:params {actorName: 'Tom Cruise', movieName: 'Top Gun'}

----

If you have set multiple parameters, you can use the `=>` operator as seen before to add more parameters to a set of parameters.

[source,cypher]
----
:param number=> 10
----

=== Using multiple parameters

Here is a different query that uses both parameters:

[source,cypher]
----
MATCH (p:Person)-[:ACTED_IN]->(m:Movie)
WHERE p.name = $actorName
AND m.title = $movieName
RETURN p, m
----

=== Viewing parameters

If you want to view the current parameters and their values, simply type:

[source,cypher]
----
:params
----

=== Removing parameters

If you want to remove an existing parameter from your session, you do so by using the JSON-style syntax and exclude the parameter for your session.

If you want to clear all parameters, you can simply type:

[source,cypher]
----
:params {}
----

== Check your understanding

include::questions/1-complete-query.adoc[leveloffset=+2]
include::questions/2-select-answer.adoc[leveloffset=+2]
include::questions/3-setting-integers.adoc[leveloffset=+2]

[.summary]
== Summary

In this lesson, you learned how to set parameter values in your Neo4j Browser session and to use parameters in queries.
In the next challenge, you will practice setting parameters.

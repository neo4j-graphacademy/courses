= Ordering Returned Results
:type: quiz
:sandbox: true

//[.video]
//video::ImKvXY72SA0[youtube,width=560,height=315]

//[.transcript]
== Ordering results

Whether you return results as nodes or as properties of nodes, you can specify a property value for the ordering.
Strings are ordered by their text values.
Boolean true comes before false when ordered.
Numeric data (including date and datetime properties) are ordered by their numeric value.

You specify `ORDER BY` in the `RETURN` clause where you specify the property for the ordering.

Here is an example:

[source,cypher]
----
MATCH (p:Person)
WHERE p.born.year = 1980
RETURN p.name AS name,
p.born AS birthDate
ORDER BY p.born
----

In this query, we find all Person nodes with a born year of 1980.
The born property of Person nodes is a Cypher Date type, so we can use the year method to test the year value.
Notice that is this code we are specifying aliases for the column headers for each row.
The rows will be ordered by the value of the born property.

The default ordering is ascending, but you can specify descending as follows:

[source,cypher]
----
MATCH (p:Person)
WHERE p.born.year = 1980
RETURN p.name AS name, p.born AS birthDate
ORDER BY p.born DESC
----

=== Eliminating null values returned

For some queries, you may want to not return null values, especially if you are ordering the data.

Here is an example:

[source,cypher]
----
MATCH (p:Person)
RETURN p.name AS name, p.born AS birthDate
ORDER BY p.born DESC
----

This query returns many rows at the beginning of result set that have a null value for the born  property.
In this case, we don't want those nodes to be retrieved.
We adjust the query as follows:

[source,cypher]
----
MATCH (p:Person)
WHERE p.born IS NOT NULL
RETURN p.name AS name, p.born AS birthDate
ORDER BY p.born DESC
----

== Ordering multiple results

You can provide multiple sort expressions and the result will be sorted in that order.

[source,cypher]
----
MATCH (p:Person)-[:DIRECTED | ACTED_IN]->(m:Movie)
WHERE p.name = 'Tom Hanks'
OR p.name = 'Keanu Reeves'
RETURN  m.year, m.title
ORDER BY m.year DESC , m.title
----

In this query we return the movies in descending year order and then the movie titles in ascending order.

There is no limit to the number of properties you can order by.

[NOTE]
--
You can order by any property or expression that is within scope of the query.
The ordering property or need not be explicitly returned, but can be used for the ordering.
--

== Check your understanding

include::questions/1-complete-query.adoc[leveloffset=+1]
include::questions/2-select-answer.adoc[leveloffset=+1]

[.summary]
== Summary

In this lesson, learned how you can order the rows returned in a query.

In the next challenge, you will write queries to order results.

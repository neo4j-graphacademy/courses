= Changing Results Returned
:type: quiz
:sandbox: true

[.video]
video::eZ4WZIKcxwU[youtube,width=560,height=315]

//https://youtu.be/eZ4WZIKcxwU

[.transcript]
== Changing data returned

You can always change the data that is returned by performing string or numeric operations on the data.

For example:

[source,cypher]
----
MATCH (m:Movie)<-[:ACTED_IN]-(p:Person)
WHERE m.title CONTAINS 'Toy Story' AND
p.died IS NULL
RETURN m.title AS movie,
p.name AS actor,
p.born AS dob,
date().year - p.born.year AS ageThisYear
----

This query returns the actors in each Toy Story movie and their age if they are still living according to our graph.
We add data to each line by calculating the actor's age.

Here is an example where we concatenate string data returned:

[source,cypher]
----
MATCH (m:Movie)<-[:ACTED_IN]-(p:Person)
WHERE m.title CONTAINS 'Toy Story' AND
p.died IS NULL
RETURN 'Movie: ' + m.title AS movie,
p.name AS actor,
p.born AS dob,
date().year - p.born.year AS ageThisYear
----

=== Conditionally changing data returned

Cypher has a `CASE` clause that you can specify to compute the data returned which may be different from what is in the graph.


Here is an example:

[source,cypher]
----
MATCH (m:Movie)<-[:ACTED_IN]-(p:Person)
WHERE p.name = 'Henry Fonda'
RETURN m.title AS movie,
CASE
WHEN m.year < 1940 THEN 'oldies'
WHEN 1940 <= m.year < 1950 THEN 'forties'
WHEN 1950 <= m.year < 1960 THEN 'fifties'
WHEN 1960 <= m.year < 1970 THEN 'sixties'
WHEN 1970 <= m.year < 1980 THEN 'seventies'
WHEN 1980 <= m.year < 1990 THEN 'eighties'
WHEN 1990 <= m.year < 2000 THEN 'nineties'
ELSE  'two-thousands'
END
AS timeFrame
----

In this query we transform the data returned to reflect the timeframe for the movie.

== Check your understanding

include::questions/1-complete-query.adoc[leveloffset=+1]
include::questions/2-select-answer.adoc[leveloffset=+1]

[.summary]
== Summary

In this lesson, you learned how you can add to or modify results returned.

In the next challenge, you will write queries to customize results returned.

= Testing strings
:type: quiz
:sandbox: true

//[.video]
//video::ImKvXY72SA0[youtube,width=560,height=315]

[.transcript]
== Testing strings

When the property is a string type, you can filter by the starting characters in the string:

[source,cypher]
----
MATCH (m:Movie)
WHERE  m.title STARTS WITH 'Toy Story'
RETURN m.title, m.released
----

This query returns the four title and released dates for all movies whose title begins with Toy Story.

And you can filter queries whose properties end with a set of characters:

[source,cypher]
----
MATCH (m:Movie)
WHERE  m.title ENDS WITH ' I'
RETURN m.title, m.released
----

This query returns five rows that meet the filtering criteria.

Additionally, you can test if a substring is contained in a property:

[source,cypher]
----
MATCH (m:Movie)
WHERE  m.title CONTAINS 'River'
RETURN m.title, m.released
----

=== Case-sensitive strings

String matching in Cypher is case-sensitive. If string values could be mis-interpreted if the letters do not match in case, your queries may miss data in the graph.
For example, the use may enter a mixed-case value, emil@Neo4j.com, which is not the same for matching purposes to emil@neo4j.com.

Here is an example query where case is significant:

[source,cypher]
----
MATCH (p:Person)
WHERE toLower(p.name) ENDS WITH 'demille'
RETURN p.name
----

We do not know if the data in the graph for a name is DeMille, Demille, or deMlle.
To ensure we match all Person nodes that could be one of these we transform the property value to lower-case so we can perform the test.
This query returns one name, Cecille B. DeMille.

Conversely, we can do the same test with upper-case:

[source,cypher]
----
MATCH (p:Person)
WHERE toUpper(p.name) ENDS WITH 'DEMILLE'
RETURN p.name
----

This query returns the same name.

Here is an example using `CONTAINS`:

[source,cypher]
----
MATCH (p:Person)
WHERE toUpper(p.name) CONTAINS ' DE '
RETURN p.name
----

In this query, we find all people that have " de", " De", or " DE" in their names.

=== Using regular expressions

You can also use regular expressions for testing string property values.

[source,cypher]
----
MATCH (u:User)
WHERE u.name =~ ".*(DVM|DDS|MD)"
RETURN u.name
----

This query returns the names of the 13 Users (reviewers of movies) who have DVM, DDS, or MD in their name.

=== About indexes for queries

If you transform a string property during a query, such as toUpper() or toLower() turns off the use of the index.

With any query, you can always check if an index will be used by prefixing the query with EXPLAIN.

[source,cypher]
----
EXPLAIN MATCH (m:Movie)
WHERE  m.title STARTS WITH 'Toy Story'
RETURN m.title, m.released
----

This query produces the execution plan where the first step is NodeIndexSeekByRange. In this case an index will be used because it is defined in the graph.

But with this query the index will not be used, even if present in the graph, because toLower() is used:

[source,cypher]
----
EXPLAIN MATCH (p:Person)
WHERE toLower(p.name) ENDS WITH 'demille'
RETURN p.name
----

Managing indexes is beyond the scope of this course, but you can read about indexes here[https://neo4j.com/docs/cypher-manual/current/indexes-for-search-performance/#administration-indexes-types^]


== Check your understanding

include::questions/1-complete-query.adoc[leveloffset=+1]
include::questions/2-select-answer.adoc[leveloffset=+1]

[.summary]
== Summary

In this lesson, you reviewed and learned about filtering string values in queries.
In the next challenge, you will write queries to filter data string data.

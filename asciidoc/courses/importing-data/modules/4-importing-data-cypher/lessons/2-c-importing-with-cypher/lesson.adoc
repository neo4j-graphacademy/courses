= Import Using Cypher
:type: challenge
:sandbox: true
:updated-at: 2022-05-02 T 13:00:00 Z

In this Challenge, you will use different CSV files that are much larger than what you have used previously.

This challenge has 7 steps:

. Delete all nodes and relationships in the graph.
. Ensure that all constraints exist in the graph.
. Import _Movie_  and _Genre_ data.
. Import _Person_ data.
. Import  the ACTED_IN relationships.
. Import  the DIRECTED relationships.
. Import _User_ data.

== Step 1: Delete all nodes and relationships in the graph

As a first step, execute this code in the sandbox to the right to remove all data in the graph.


[source,Cypher]
----
MATCH (n) DETACH DELETE n
----

== Step 2: Ensure all constraints exist in the graph

Execute this code in the sandbox to the right to show the constraints in the graph.

[source,Cypher]
----
SHOW CONSTRAINTS
----

You *must have four uniqueness constraints* defined for:

* _Person.tmdbId_
* _Movie.movieId_
* _User.userId_
* _Genre.name_

image::images/constraints-created.png[Constraints created,width=800,align=center]

[NOTE]
If Person, Movie, and User constraints were previously created by the Data Importer, the names will be different, but the constraints should be in the graph and it is alright if they have different names.

For example, here is the code to create _Genre_ constraint:

[source,Cypher]
----
CREATE CONSTRAINT Genre_name IF NOT EXISTS
ON (x:Genre)
REQUIRE x.name IS UNIQUE
----

== Step 3: Import _Movie_  and _Genre_ data

As a first step, execute this code in the sandbox to the right so that you can verify that the movie data is being property transformed from the CSV file:

[source,Cypher]
----
LOAD CSV WITH HEADERS
FROM 'https://data.neo4j.com/importing/2-movieData.csv'
AS row
//process only Movie rows
WITH row WHERE row.Entity = "Movie"
RETURN
toInteger(row.tmdbId),
toInteger(row.imdbId),
toFloat(row.imdbRating),
row.released,
row.title,
toInteger(row.year),
row.poster,
toInteger(row.runtime),
split(coalesce(row.countries,""), "|"),
toInteger(row.imdbVotes),
toInteger(row.revenue),
row.plot,
row.url,
toInteger(row.budget),
split(coalesce(row.languages,""), "|"),
split(coalesce(row.genres,""), "|")
LIMIT 10
----

This is the Cypher code for first pass we will make through the *2-movieData.csv* file to create the _Movie_, _Person_, and _Genre_ nodes.
Notice in this code we perform all of the necessary transformations of types when we set the properties for the _Movie_ node.
We use `MERGE` to only create the _Movie_ and _Genre_ nodes if they do not already exist.
And we create the _IN_GENRE_ relationships.

Execute this code in the sandbox to the right to read the CSV data and create the _Movie_ and _Genre_ nodes:

[source,Cypher]
----
:auto USING PERIODIC COMMIT
LOAD CSV WITH HEADERS
FROM 'https://data.neo4j.com/importing/2-movieData.csv'
AS row
//process only Movie rows
WITH row WHERE row.Entity = "Movie"
MERGE (m:Movie {movieId: toInteger(row.movieId}))
ON CREATE SET
m.tmdbId = toInteger(row.tmdbId),
m.imdbId = toInteger(row.imdbId),
m.imdbRating = toFloat(row.imdbRating),
m.released = row.released,
m.title = row.title,
m.year = toInteger(row.year),
m.poster = row.poster,
m.runtime = toInteger(row.runtime),
m.countries = split(coalesce(row.countries,""), "|"),
m.imdbVotes = toInteger(row.imdbVotes),
m.revenue = toInteger(row.revenue),
m.plot = row.plot,
m.url = row.url,
m.budget = toInteger(row.budget),
m.languages = split(coalesce(row.languages,""), "|")
WITH m,split(coalesce(row.genres,""), "|") AS genres
UNWIND genres AS genre
WITH m, genre
MERGE (g:Genre {name:genre})
MERGE (m)-[:IN_GENRE]->(g)
----

When you execute this code you should see:

Added 9145 labels, created 9145 nodes, set 146020 properties, created 20340 relationships.

[NOTE]
You may encounter a _Neo.ClientError.Transaction.TransactionTimedOut_ error. This means that only part of the import was committed to the graph.
You can simply rerun the code, but the number of nodes, labels, properties, relationships created may be different.

== Step 4: Import _Person_ data

As a first step, execute this code in the sandbox to the right so that you can verify that the person data is being property transformed from the CSV file:

[source,Cypher]
----
LOAD CSV WITH HEADERS
FROM 'https://data.neo4j.com/importing/2-movieData.csv'
AS row
WITH row WHERE row.Entity = "Person"
RETURN
toInteger(row.tmdbId),
toInteger(row.imdbId),
row.bornIn,
row.name,
row.bio,
row.poster,
row.url,
CASE row.born WHEN "" THEN null ELSE date(row.born) END,
CASE row.died WHEN "" THEN null ELSE date(row.died) END
LIMIT 10
----

This is the Cypher code for second pass we will make through the 2-movieData.csv file to create the _Person_ nodes for actors.
Notice in this code we perform all the necessary transformations of types when we set the properties for the _Person_ node.
We use `MERGE` to only create the _Person_ nodes if they do not already exist. We also set the _Actor_ label and create the _ACTED_IN_ relationships and set the _role_ property for the relationship.

Execute this code in the sandbox on the right.

[source,Cypher]
----
:auto USING PERIODIC COMMIT
LOAD CSV WITH HEADERS
FROM 'https://data.neo4j.com/importing/2-movieData.csv'
AS row
WITH row WHERE row.Entity = "Person"
MERGE (p:Person {tmdbId: toInteger(row.tmdbId)})
ON CREATE SET
p.imdbId = toInteger(row.imdbId),
p.bornIn = row.bornIn,
p.name = row.name,
p.bio = row.bio,
p.poster = row.poster,
p.url = row.url,
p.born = CASE row.born WHEN "" THEN null ELSE date(row.born) END,
p.died = CASE row.died WHEN "" THEN null ELSE date(row.died) END
----

When you execute this code, you should see:

Added 19047 labels, created 19047 nodes, set 119195 properties

[NOTE]
You may encounter a _Neo.ClientError.Transaction.TransactionTimedOut_ error. This means that only part of the import was committed to the graph.
You can simply rerun the code, but the number of nodes, labels, properties, relationships created may be different.

== Step 5: Import  the ACTED_IN relationships

As a first step, execute this code in the sandbox to the right to see what data is being read from the CSV file:

[source,Cypher]
----
LOAD CSV WITH HEADERS
FROM 'https://data.neo4j.com/importing/2-movieData.csv'
AS row
WITH row WHERE row.Entity = "Join" AND row.Work = "Acting"
RETURN
toInteger(row.tmdbId),
toInteger(row.movieId),
row.role
LIMIT 10
----

This is the Cypher code for third pass we will make through the 2-movieData.csv file to create ACTED_IN relationships in the graph.
We also add the _Actor_ label to the _Person_ node. Execute this code in the sandbox on the right.

[source,Cypher]
----
:auto USING PERIODIC COMMIT
LOAD CSV WITH HEADERS
FROM 'https://data.neo4j.com/importing/2-movieData.csv'
AS row
WITH row WHERE row.Entity = "Join" AND row.Work = "Acting"
MATCH (p:Person {tmdbId: toInteger(row.tmdbId}))
MATCH (m:Movie {movieId: toInteger(row.movieId}))
MERGE (p)-[r:ACTED_IN]->(m)
ON CREATE
SET r.role = row.role
SET p:Actor
----

When you execute this code, you should see:

Added 15443 labels, set 34274 properties, created 35910 relationships

[NOTE]
You may encounter a _Neo.ClientError.Transaction.TransactionTimedOut_ error. This means that only part of the import was committed to the graph.
You can simply rerun the code, but the number of nodes, labels, properties, relationships created may be different.

== Step 6: Import  the DIRECTED relationships

As a first step, execute this code in the sandbox to the right to see what data is being read from the CSV file:

[source,Cypher]
----
LOAD CSV WITH HEADERS
FROM 'https://data.neo4j.com/importing/2-movieData.csv'
AS row
WITH row WHERE row.Entity = "Join" AND row.Work = "Directing"
RETURN
toInteger(row.tmdbId),
toInteger(row.movieId),
row.role
LIMIT 10
----

There are some rows in the CSV file where a value of "Directing" _Work_ could have an associated role value.
Modify the above query to show such rows.

*Hint:* Add `AND exists(row.role)` to the `WHERE` clause.


This is the Cypher code for forth pass we will make through the 2-movieData.csv file to create DIRECTED relationships in the graph.
We also add the _Director_ label to the _Person_ node. Execute this code in the sandbox on the right.

[source,Cypher]
----
:auto USING PERIODIC COMMIT
LOAD CSV WITH HEADERS
FROM 'https://data.neo4j.com/importing/2-movieData.csv'
AS row
WITH row WHERE row.Entity = "Join" AND row.Work = "Directing"
MATCH (p:Person {tmdbId: toInteger(row.tmdbId)})
MATCH (m:Movie {movieId: toInteger(row.movieId}))
MERGE (p)-[r:DIRECTED]->(m)
ON CREATE
SET r.role = row.role
SET p:Director
----

When you execute this code, you should see:

Added 4091 labels, set 1152 properties, created 10007 relationships

== Step 7: Import the _User_ data

The *2-ratingData.csv* file contains data for users who rated movies.

As a first step, execute this code in the sandbox to the right to see what data is being read from the CSV file:

[source,Cypher]
----
LOAD CSV WITH HEADERS
FROM 'https://data.neo4j.com/importing/2-ratingData.csv'
AS row
RETURN
toInteger(row.movieId),
toInteger(row.userId),
row.name,
toInteger(row.rating),
toInteger(row.timestamp)
LIMIT 100
----


Here is the code to create the users and _RATED_ relationships.

Execute this code in the sandbox on the right.

[source,Cypher]
----
:auto USING PERIODIC COMMIT
LOAD CSV WITH HEADERS
FROM 'https://data.neo4j.com/importing/2-ratingData.csv'
AS row
MATCH (m:Movie {movieId: toInteger(row.movieId})
MERGE (u:User {userId: toInteger(row.userId}))
ON CREATE SET u.name = row.name
MERGE (u)-[r:RATED]->(m)
ON CREATE SET r.rating = toInteger(row.rating),
r.timestamp = toInteger(row.timestamp)
----

When you execute this code, you should see:

Added 671 labels, created 671 nodes, set 201350 properties, created 100004 relationships

[NOTE]
You may encounter a _Neo.ClientError.Transaction.TransactionTimedOut_ error. This means that only part of the import was committed to the graph.
You can simply rerun the code, but the number of nodes, labels, properties, relationships created may be different.

include::./questions/verify.adoc[leveloffset=+1]

[.summary]
== Summary

In this challenge, you imported a large dataset using Cypher.

This concludes your introduction to importing CSV data into Neo4j.


= Relationships
:order: 4
:type: lesson

[.video]
video::YouTubeVideoID


[.transcript]

In this lesson, you will create `ACTED_IN` and `DIRECTED` relationships between the Person and Movie nodes.

To complete this lesson, you will need to:
- `LOAD CSV` data from the link:https://data.neo4j.com/importing-cypher/acted_in.csv[`acted_in.csv`] and link:https://data.neo4j.com/importing-cypher/directed.csv[`directed.csv`] files.
- `MATCH` the appropriate `Person` and `Movie` nodes.
- Use `MERGE` to create the `ACTED_IN` or `DIRECTED` relationship between the matched `Person` and `Movie` nodes.

== ACTED_IN relationships

The link:https://data.neo4j.com/importing-cypher/acted_in.csv[`acted_in.csv`] file contains the following data:

* movieId - the `movieId` property of the `Movie` node
* person_tmdbId - the `tmbdId` property of the `Person` node
* role - the role the person played in the movie

Review the Cypher statement below, which creates the `ACTED_IN` relationships between the `Person` and `Movie` nodes.

[source, cypher]
----
LOAD CSV WITH HEADERS
FROM 'https://data.neo4j.com/importing-cypher/acted_in.csv' AS row
MATCH (p:Person {tmdbId: toInteger(row.person_tmdbId)})
MATCH (m:Movie {movieId: toInteger(row.movieId)})
MERGE (p)-[r:ACTED_IN]->(m)
SET r.role = row.role
----

Try to identify:

* 2 `MATCH` clauses use the `movieId` and `person_tmdbId` properties to find the appropriate `Person` and `Movie` nodes.
* The `MERGE` clause that creates the `ACTED_IN` relationship between the matched `p` and `m` nodes 
* The `SET` clause that sets the `role` property of the `ACTED_IN` relationship `r`.

Run the Cypher statement to create the `ACTED_IN` relationships.

To verify that you created the `ACTED_IN` relationships successfully, run the following Cypher statement:

[source, cypher]
----
MATCH (p:Person)-[r:ACTED_IN]->(m:Movie) RETURN p, r, m LIMIT 25
----

== DIRECTED relationships

Your challenge is to write a similar Cypher statement to create the `DIRECTED` relationship between the `Person` and `Movie` nodes.

The link:https://data.neo4j.com/importing-cypher/directed.csv[`directed.csv`] file contains just the `movieId` and `person_tmdbId` IDs.

Unlike the `ACTED_IN` relationship, the `DIRECTED` relationship has no properties (e.g. `role`), so you will not need to use the `SET` clause.

To verify that you created the `DIRECTED` relationship successfully, run the following Cypher statement:

[source, cypher]
----
MATCH (p:Person)-[r:DIRECTED]->(m:Movie) RETURN p, r, m LIMIT 25
----

[TIP]
.Directed and acted in
====
Now you have created the `ACTED_IN` and `DIRECTED` relationships, you can use the data to find people who directed and acted in the same movie.

[source, cypher]
----
MATCH (p:Person)-[:ACTED_IN]->(m:Movie)<-[:DIRECTED]-(p:Person)
RETURN p, m
----
====

[.quiz]
== Check Your Understanding

include::./questions/verify.adoc[leveloffset=+1]

[.summary]
== Summary

In this lesson, you learned how to create relationships between nodes from data in a CSV file.

In the next lesson, you will complete a quiz based on what you learned.
= Data Types
:order: 1
:type: lesson

Explore how the property types are all strings. How to determine a property type.

Casting properties to the correct data type in the import of the Person node

Understand why properties are all strings. How to set a data type at import.



The types of data that you can store as properties in Neo4j include:

* String
* Long (integer values)
* Double (decimal values)
* Boolean
* Date/Datetime
* Point (spatial)
* StringArray (comma-separated list of strings)
* LongArray (comma-separated list of integer values)
* DoubleArray (comma-separated list of decimal values)

You can use this Cypher code to show the stored types for the node properties in the graph:

CALL apoc.meta.nodeTypeProperties()
YIELD nodeType, propertyName, propertyTypes




[.video]
video::YouTubeVideoID


[.transcript]

In this lesson, you will explore how to cast data from a CSV file to different data types in Neo4j.

All data loaded using `LOAD CSV` will be returned as strings - you need to cast the data to an appropriate data type before being written to a property. 

The types of data that you can store as properties in Neo4j include:

* String
* Long (integer values)
* Float (decimal values)
* Boolean
* Date/Datetime
* Point (spatial)
* Lists of values

== Casting

There are Cypher functions to cast data to appropriate types. For example, when creating the `Person` nodes, you used the `toInteger()` function to cast IDs to integers.

[source, cypher]
----
...
MERGE (p:Person {tmdbId: toInteger(row.person_tmdbId)})
SET
p.imdbId = toInteger(row.person_imdbId)
----

Cypher functions to cast data include:

|===
|Function |Description

|`toBoolean()` |Converts a string to a boolean value
|`toFloat()` |Converts a string to a float value
|`toInteger()` |Converts a string to an integer value
|`toString()` |Converts a value to a string
|`date()` |Converts a string to a date value
|`datetime()` |Converts a string to a date and time value
|===

Run the following Cypher code to show the data types used in the graph:

[source, cypher]
----
CALL apoc.meta.nodeTypeProperties()
YIELD nodeType, propertyName, propertyTypes
----

Review the results and note that the data types for `Person`, `born` and `died` are strings.

[source]
----
":`Person`"	"died"	["String"]
":`Person`"	"born"	["String"]
----

== Person node dates

The `Person` nodes have `born` and `died` properties are both dates. You can use the `date()` function to convert the strings to date values.

This is the code you used to create the `Person` nodes:

[source, cypher]
----
LOAD CSV WITH HEADERS FROM "https://data.neo4j.com/importing-cypher/persons.csv" AS row
MERGE (p:Person {tmdbId: toInteger(row.person_tmdbId)})
SET
p.imdbId = toInteger(row.person_imdbId),
p.bornIn = row.bornIn,
p.name = row.name,
p.bio = row.bio,
p.poster = row.poster,
p.url = row.url,
p.born = row.born,
p.died = row.died
----

You can modify it to use the `date()` function to convert the `born` and `died` properties to `Date` values:

[source, cypher]
----
LOAD CSV WITH HEADERS FROM "https://data.neo4j.com/importing-cypher/persons.csv" AS row
MERGE (p:Person {tmdbId: toInteger(row.person_tmdbId)})
SET
p.imdbId = toInteger(row.person_imdbId),
p.bornIn = row.bornIn,
p.name = row.name,
p.bio = row.bio,
p.poster = row.poster,
p.url = row.url,
p.born = date(row.born),
p.died = date(row.died)
----

Run the query to update the `Person` nodes with the `born` and `died` properties as `Date` values.

The remaining properties are all string values, so casting them to a different data type is unnecessary.

[.quiz]
== Check Your Understanding

include::questions/verify.adoc[leveloffset=+1]

[.summary]
== Summary

In this lesson, you learned how to cast data to different data types in Neo4j.

In the next lesson, you will update the `Movie` nodes to use the relevant data type for each property.
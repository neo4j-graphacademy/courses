= Data Types
:order: 1
:type: lesson
:sandbox: true

[.video]
video::YouTubeVideoID


[.transcript]

In this lesson, you will explore how to cast data from a CSV file to different data types in Neo4j.

All data loaded using `LOAD CSV` will be returned as strings - you need to cast the data to an appropriate data type before being written to a property. 

The types of data that you can store as properties in Neo4j include:

* String
* Long (integer values)
* Float (decimal values)
* Boolean
* Date/Datetime
* Point (spatial)
* Lists of values

== Casting

There are Cypher functions to cast data to appropriate types. For example, when creating the `Person` nodes, you used the `toInteger()` function to cast IDs to integers.

[source, cypher]
----
...
MERGE (p:Person {tmdbId: toInteger(row.person_tmdbId)})
SET
p.imdbId = toInteger(row.person_imdbId)
----

Cypher functions to cast data include:

|===
|Function |Description

|`toBoolean()` |Converts a string to a boolean value
|`toFloat()` |Converts a string to a float value
|`toInteger()` |Converts a string to an integer value
|`toString()` |Converts a value to a string
|`date()` |Converts a string to a date value
|`datetime()` |Converts a string to a date and time value
|===

You can use the `apoc.meta.nodeTypeProperties()` function to show the data types used in the graph:

[source, cypher]
----
CALL apoc.meta.nodeTypeProperties()
YIELD nodeType, propertyName, propertyTypes
----

Review the results and note, except for the IDs, that the data types for properties of `Person` are all strings.

|===
|Node Type |Property |Data Type

|":`Person`" |"tmdbId" |["Long"]
|":`Person`" |"imdbId" |["Long"]
|":`Person`" |"bornIn" |["String"]
|":`Person`" |"born"   |["String"]
|":`Person`" |"name"   |["String"]
|":`Person`" |"bio"    |["String"]
|":`Person`" |"died"   |["String"]
|===

[NOTE]
Neo4j uses the data type `Long` for integer values.

== Person node dates

The `Person` node `born` and `died` properties are both dates. You can use the `date()` function to convert the strings to date values.

You used this Cypher statement to create the `Person` nodes:

[source, cypher]
----
LOAD CSV WITH HEADERS FROM "https://data.neo4j.com/importing-cypher/persons.csv" AS row
MERGE (p:Person {tmdbId: toInteger(row.person_tmdbId)})
SET
p.imdbId = toInteger(row.person_imdbId),
p.bornIn = row.bornIn,
p.name = row.name,
p.bio = row.bio,
p.poster = row.poster,
p.url = row.url,
p.born = row.born,
p.died = row.died
----

You can modify it to use the `date()` function to convert the `born` and `died` properties to `Date` values:

[source, cypher]
----
LOAD CSV WITH HEADERS FROM "https://data.neo4j.com/importing-cypher/persons.csv" AS row
MERGE (p:Person {tmdbId: toInteger(row.person_tmdbId)})
SET
p.imdbId = toInteger(row.person_imdbId),
p.bornIn = row.bornIn,
p.name = row.name,
p.bio = row.bio,
p.poster = row.poster,
p.url = row.url,
p.born = date(row.born),
p.died = date(row.died)
----

Run the query to update the `Person` nodes with the `born` and `died` properties as `Date` values.

Use the `apoc.meta.nodeTypeProperties` function again to check that the `born` and `died` properties are now `Date` values:

[source, cypher]
----
CALL apoc.meta.nodeTypeProperties()
YIELD nodeType, propertyName, propertyTypes
----

[TIP]
.Using MERGE not CREATE?
As `MERGE` was used in this Cypher statement, you can run it multiple times without creating duplicate nodes. It will update the existing nodes with the new date values. If you used `CREATE` instead, you would create new nodes each time you ran the statement.
 
The remaining properties are all string values, so casting them to a different data type is unnecessary.

[.quiz]
== Check Your Understanding

include::questions/verify.adoc[leveloffset=+1]

[.summary]
== Summary

In this lesson, you learned how to cast data to different data types in Neo4j.

In the next lesson, you will update the `Movie` nodes to use the relevant data type for each property.
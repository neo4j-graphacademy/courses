= Creating lists
:type: quiz
:sandbox: true

//[.video]
//video::dyAJ_H1eixk[youtube,width=560,height=315]

//https://youtu.be/dyAJ_H1eixk

[.transcript]
== What can a list contain?

A list is an array that contains elements. The elements in a list need not all be of the same type.
Here is an example:

[source,cypher]
----
MATCH (m:Movie)
RETURN [m.title, m.released, date().year - date(m.released).year + 1 ]
----

This query returns one row for each movie in the graph. The data in each row is a list that contains the title of the movie, the year the movie was released, and the number of years ago the movie was released.

Most queries use `collect() to create lists, but there are other functions you can use that also create lists, such as `nodes()`.
The list you create can be something that you return, like the titles of movies:

[source,cypher]
----
MATCH (a:Actor)--(m:Movie)
WITH a, collect (m.title) as Movies
RETURN a.name, Movies LIMIT 10
----

== How `collect()` works

//Andrew's video: look at query at 10:50 as starting point, then move collect up: https://www.youtube.com/watch?v=QnozzFP_fPo

`collect()` returns a list of elements. You can use `collect()` to create a list at any time during your query.
When you create a list during a query, aggregation occurs.

Here is an example. Execute this query to see the query plan:

[source,cypher]
----
PROFILE
MATCH (m:Movie {title:'Jupiter Ascending'})
MATCH (d:Person)-[:DIRECTED]->(m)
MATCH (a:Person)-[:ACTED_IN]->(m)
RETURN m.title as Title, collect(DISTINCT a.name) as Actors,
collect(DISTINCT d.name) as Directors
----

What  you must remember is that each `MATCH` in a query returns rows that are operated on by the next step of the query.

In this query, the first `MATCH` returns one row, for the movie, Jupiter Ascending.
The second `MATCH` uses the row from the previous `MATCH` and then returns two rows, one for each person who directed the movie.
The third `MATCH` uses the two rows from the previous `MATCH and then returns eight rows. This is because for each row for the movie, Jupiter Ascending, there are four actors.
Aggregation (OrderedAggregation step in the query plan) occurs during the `RETURN` clause.
All names of actors are collected or aggregated, as well as all names of directors.
When aggregation occurs, the graph engine selects a grouping key.
In this example the movie title is the grouping key.
All data collected into the Actors and Directors will be grouped by the movie title.
In the `RETURN` we also specify `DISTINCT` so that we will not have duplicate names in the lists returned.
If we had not specified `DISTINCT` for the aggregation, we would have seen multiple actor names, one for each row in the third `MATCH` clause.

You can imagine that in a graph with many relationships, a query tuning goal should be to reduce the number of rows processed in the query (looking at the query plan).
Even if you perform a single MATCH, you will see that the same query plan is used:

[source,cypher]
----
PROFILE
MATCH (d:Person)-[:DIRECTED]->(m:Movie {title:'Jupiter Ascending'})<-[:ACTED_IN]-(a:Person)
RETURN m.title as Title, collect(DISTINCT a.name) as Actors,
collect(DISTINCT d.name) as Directors
----

To reduce the number of rows processed, we can move the aggregation up in the query as follows:

[source,cypher]
----
PROFILE
MATCH (m:Movie {title:'Jupiter Ascending'})
MATCH (d:Person)-[:DIRECTED]->(m)
WITH m, collect (d.name) as Directors
MATCH (a:Person)-[:ACTED_IN]->(m)
RETURN m.title as Title, collect(a.name) as Actors
----

After the second `MATCH` we collect the row into a list.
Notice that in the query plan the **EagerAggregation** operator executes.
We pass on the Directors list, along with the Movie node for the next `MATCH`.
Notice that in the query plan, only one row is passed into the third `MATCH` clause.
We no longer need to use `DISTINCT` because there is no longer duplicate data in the rows.

In your query tuning, you should strive to reduce the number of rows processed during a query.

=== Grouping key

During aggregation, the graph engine typically groups data based upon some value in a row.
Here is a query where each row is returned by the name of the actor and the aggregation is done for each actor.
The grouping key is a.name.

[source,cypher]
----
PROFILE MATCH (a:Person)-[:ACTED_IN]->(m:Movie)
RETURN a.name AS Actor, collect(m.title) AS Movies LIMIT 100
----

== Collecting nodes

You can also collect nodes so that you may use the list of nodes for later processing in the query.
Here is a query where we create Language nodes from data in the Movie nodes:

[source,cypher]
----
MATCH (m:Movie)
UNWIND m.languages AS language
WITH language, collect(m) AS movies
MERGE (l:Language {name:language})
WITH l, movies
UNWIND movies AS m
WITH l,m
MERGE (l)<-[:SPEAKS]-(m)
// NOTE: to undo these merges:
// MATCH (x:Language) DETACH DELETE x
----

Here you see in this code the following:

. The first `MATCH` retrieves all Movie nodes.
. Each value in the languages property is returned.
. We then collect the movie nodes and use the value of language as the grouping key. That is all of the movies with a value of English will be collected into a list.
. We then create the Language node using the language value.
. With the language value and the list of Movie nodes for that language, we unwind the movie nodes into rows.
. With each language and row for each Movie, we create the relationship between the Language node and the Movie node.

This code collects nodes using the language value so that each node can be used to create the relationship.
In this type of query, you typically use collect() to collect nodes and UNWIND lists so they can be processed later in the query.

== Collecting relationships

Here is an example where we collect relationships, then unwind them to query and return the correct values:

[source,cypher]
----
MATCH (u:User {name: "Misty Williams"})-[x]->()
WITH collect(x) AS ratings
UNWIND ratings AS r
WITH r WHERE r.rating <= 1
RETURN r.rating AS Rating, endNode(r).title AS Title
----

Notice here that we use endNode() to return the node at the end of the relationship.

== Collecting paths in a query

Here is an example where we perform a query and use the `nodes()` function to return the list of all paths in the  query:

[source,cypher]
----
// Movies in all paths that begin with Elvis and end with an Actor that are 4 hops away from Elvis
MATCH path = (p:Person {name: 'Elvis Presley'})-[*4]-(a:Actor)
WITH nodes(path) as n
UNWIND n as x
WITH x where x:Movie
RETURN DISTINCT x.title
----

 In the above query, there is a mixture of Person and Movie nodes in the path.
 We specify a criteria that we only want to look at Movie nodes.

== collect() vs. subquery

A subquery is a good way to aggregate data for later processing in your queries.
Suppose we have this query which returns 208 rows:

[source,cypher]
----
PROFILE
// need to come up with a query without subquery that returns same results as below
----

It can be rewritten as follows using a subquery:

[source,cypher]
----
PROFILE MATCH (m:Movie)
CALL {
WITH m
MATCH (m)<-[:ACTED_IN]-(a:Person)
WITH a LIMIT 3
RETURN collect(a.name) AS Actors
}
RETURN m.title AS Title, Actors
----

== Check your understanding

include::questions/1-select-answer.adoc[leveloffset=+1]

[.summary]
== Summary

In this lesson, you learned about the types of constraints and indexes that Neo4j supports.
In the next lesson, you will learn about determining what constraints and indexes to create in your graph.

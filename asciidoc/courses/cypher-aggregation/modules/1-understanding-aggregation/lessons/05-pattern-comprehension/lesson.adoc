= Using Pattern Comprehension
:type: quiz
:sandbox: true

[.video]
video::XQq3Ap7xDNQ[youtube,width=560,height=315]

//https://youtu.be/XQq3Ap7xDNQ

[NOTE]
This video was recorded using an earlier version of the sandbox (Neo4j 4.x).
You are using a Neo4j 5.4 sandbox for this course.
You will see some differences in the PROFILE results for db hits and elapsed time in Neo4j 5.4.

[.transcript]

== Using pattern comprehension

Pattern comprehension in Cypher enables you to create a list based upon values obtained during a query.
It is implemented in Cypher as a subquery.

Here is how we aggregate using the `collect()` function:

[source,cypher]
----
PROFILE
MATCH(m:Movie)
WHERE m.year = 2015
OPTIONAL MATCH (a:Person)-[:ACTED_IN]-(m)
WITH m, collect(DISTINCT a.name) AS Actors
OPTIONAL MATCH (m)-[:DIRECTED]-(d:Person)
RETURN m.title AS Movie, Actors, collect (DISTINCT d.name) AS Directors
----

Using Cypher's pattern comprehension syntax: `[<pattern> | value]`, we dynamically return a list using values obtained from the pattern.
In some cases, pattern comprehension performs better than using `collect()` because it uses a subquery.

Here is the same query using pattern comprehension:

[source,cypher]
----
PROFILE
MATCH (m:Movie)
WHERE m.year = 2015
RETURN m.title AS Movie,
[(dir:Person)-[:DIRECTED]->(m) | dir.name] AS Directors,
[(actor:Person)-[:ACTED_IN]->(m) | actor.name] AS Actors
----

// video redo
// need to re-record this for elapsed time rather than db hits

// reco db change impact
// Change to:
//The behavior of pattern comprehension is slightly better than using OPTIONAL MATCH and for this particular query.

//The behavior of pattern comprehension is similar to using OPTIONAL MATCH and for this particular query, the elapsed time is slightly reduced, but the number of db hits remains the same.
The behavior of pattern comprehension is slightly better than using OPTIONAL MATCH and for this particular query.
A benefit of pattern comprehension is that you don't need to have a collect() stage and you avoid potential cartesian products.

=== Filtering during pattern comprehension

You can specify a filtering criteria for the pattern:

[source,cypher]
----
MATCH (a:Person {name: 'Tom Hanks'})
RETURN [(a)--(b:Movie)
WHERE b.title CONTAINS "Toy" | b.title ]
AS Movies
----

=== Adding more context to the list returned

You can even provide more context to the elements of the list returned where we add the year to the movie title for each element of the list:

[source,cypher]
----
MATCH (a:Person {name: 'Tom Hanks'})
RETURN [(a)--(b:Movie)
WHERE b.title CONTAINS "Toy" | b.title + ": " + b.year]
AS Movies
----

== Check your understanding

include::questions/1-pattern-comprehension.adoc[leveloffset=+1]
include::questions/2-pattern-comprehension.adoc[leveloffset=+1]

[.summary]
== Summary

In this lesson, you reviewed and learned more about aggregating data  pattern comprehension your Cypher queries.
In the next Challenge, you will create a query  to aggregate using pattern comprehension.
